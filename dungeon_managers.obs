use System.Concurrency;
use Collection.Generic;
use Game.SDL2;
use Game.Framework;
use System.IO.File;

class Manager {
	method : virtual : public : Load() ~ Bool;
	method : virtual : public : Unload() ~ Nil;
}

class MapHolder {
	@map : Byte[,];
	@map_start : Int[];

	New(map : Byte[,], map_start : Int[]) {
		@map := map;
		@map_start := map_start;
	}

	method : public : Get() ~ Byte[,] {	
		return @map;
	}

	method : public : GetStart() ~ Int[] {
		return @map_start;
	}
}

#~
Managers map operations
~#
class MapManager from Manager {
	@maps : Vector<MapHolder>;
	@map_index : Int;
	@path : String;
	@player_view : Int[,];

	New(path : String) {
		Parent();

		@path := "{$path}/debug.map";
		@maps := Vector->New()<MapHolder>;
		@player_view := Int->New[4, 3];
	}	

	method : public : Load() ~ Bool {
		return LoadMaps();
	}

	method : public : GetStart() ~ Int[] {
		return @maps->Get(@map_index)->GetStart();
	}

	method : GetMap() ~ Byte[,] {
		return @maps->Get(@map_index)->Get();
	}

	method : public : GetView(dir : MapManager->Direction,  y : Int, x : Int) ~ Int[,] {
		# north
		if(dir = MapManager->Direction->NORTH) {
			# first
			@player_view[0,0] := GetType(y, x - 1);
			@player_view[0,1] := GetType(y, x);
			@player_view[0,2] := GetType(y, x + 1);

			# second
			@player_view[1,0] := GetType(y - 1, x - 1);
			@player_view[1,1] := GetType(y - 1, x);
			@player_view[1,2] := GetType(y - 1, x + 1);

			# thrid
			@player_view[2,0] := GetType(y - 2, x - 1);
			@player_view[2,1] := GetType(y - 2, x);
			@player_view[2,2] := GetType(y - 2, x + 1);

			# forth
			@player_view[3,0] := GetType(y - 3, x - 1);
			@player_view[3,1] := GetType(y - 3, x);
			@player_view[3,2] := GetType(y - 3, x + 1);
		}
		# south
		else if(dir = MapManager->Direction->SOUTH) {
			# first
			@player_view[0,0] := GetType(y, x - 1);
			@player_view[0,1] := GetType(y, x);
			@player_view[0,2] := GetType(y, x + 1);

			# second
			@player_view[1,0] := GetType(y + 1, x - 1);
			@player_view[1,1] := GetType(y + 1, x);
			@player_view[1,2] := GetType(y + 1, x + 1);

			# thrid
			@player_view[2,0] := GetType(y + 2, x - 1);
			@player_view[2,1] := GetType(y + 2, x);
			@player_view[2,2] := GetType(y + 2, x + 1);

			# forth
			@player_view[3,0] := GetType(y + 3, x - 1);
			@player_view[3,1] := GetType(y + 3, x);
			@player_view[3,2] := GetType(y + 3, x + 1);
		}
		# east
		else if(dir = MapManager->Direction->EAST) {
			# first
			@player_view[0,0] := GetType(y - 1, x);
			@player_view[0,1] := GetType(y, x);
			@player_view[0,2] := GetType(y + 1, x);

			# second
			@player_view[1,0] := GetType(y - 1, x + 1);
			@player_view[1,1] := GetType(y, x + 1);
			@player_view[1,2] := GetType(y + 1, x + 1);

			# thrid
			@player_view[2,0] := GetType(y - 1 , x + 2);
			@player_view[2,1] := GetType(y, x + 2);
			@player_view[2,2] := GetType(y + 1, x + 2);

			# forth
			@player_view[3,0] := GetType(y - 1, x + 3);
			@player_view[3,1] := GetType(y, x + 3);
			@player_view[3,2] := GetType(y + 1, x + 3);
		}
		# west
		else {
			# first
			@player_view[0,0] := GetType(y - 1, x);
			@player_view[0,1] := GetType(y, x);
			@player_view[0,2] := GetType(y + 1, x);

			# second
			@player_view[1,0] := GetType(y - 1, x - 1);
			@player_view[1,1] := GetType(y, x - 1);
			@player_view[1,2] := GetType(y + 1, x - 1);

			# thrid
			@player_view[2,0] := GetType(y - 1 , x - 2);
			@player_view[2,1] := GetType(y, x - 2);
			@player_view[2,2] := GetType(y + 1, x - 2);

			# forth
			@player_view[3,0] := GetType(y - 1, x - 3);
			@player_view[3,1] := GetType(y, x - 3);
			@player_view[3,2] := GetType(y + 1, x - 3);
		};

		return @player_view;
	}

	method : public : GetType(y : Int, x : Int) ~ Int {
		if(x < 0 | x >= Type->SIZE | y < 0 | y >= Type->SIZE) {
			return Type->NONE;
		};

		map := GetMap();
		return map[y, x]->As(Int);
	}

	method : LoadMaps() ~ Bool {
		map_file := FileReader->New(@path);
		leaving {
			map_file->Close();
		};

		end_line := map_file->ReadString();
		while(<>end_line->StartsWith('-')) {
			rows := cols := Type->SIZE;

			map := Byte->New[rows, cols];
			map_start := Int->New[2];

			for(i := 0; i < rows; i += 1;) {
				line := map_file->ReadString();
				if(line->Size() <> cols) {
					return false;
				};

				each(j : line) {
					select(line->Get(j)) {
						# clear
						label '@' {
							map[i,j] := MapManager->Type->CLEAR;
						}
						# start
						label 'S' {
							map[i,j] := MapManager->Type->START;
							map_start[0] := i; map_start[1] := j;
						}
						# end
						label 'E' {
							map[i,j] := MapManager->Type->END;
						}
						# chest
						label 'C' {
							map[i,j] := MapManager->Type->CHEST;
						}
						# potion
						label 'P' {
							map[i,j] := MapManager->Type->POTION;
						}
						# trap
						label 'T' {
							map[i,j] := MapManager->Type->TRAP;
						}
						# warp
						label 'W' {
							map[i,j] := MapManager->Type->WARP;
						}
					};
				};
			};

			@maps->AddBack(MapHolder->New(map, map_start));
			end_line := map_file->ReadString();
		};

		return true;
	}

	method : public : Unload() ~ Nil {
	}

	consts Type {
		SIZE := 18,
		BLOCKED := 0,
		CLEAR := 1,
		START := 2,
		END := 3,
		DOOR := 4,
		POTION := 5,
		CHEST := 6,
		TRAP := 7,
		WARP := 8,
		NONE := 9
	}

	enum Direction {
		NORTH,
		SOUTH,
		EAST,
		WEST
	}

	enum Side {
		LEFT,
		MIDDLE,
		RIGHT
	}
}

#~
Managers sprite images
~#
class SpriteManager from Manager {
	@framework : GameFramework;
	@dungeon_sprites : AnimatedImageSprite;

	New(path : String, framework : GameFramework) {
		Parent();
		@framework := framework;
		@dungeon_sprites := @framework->AddAnimatedImageSprite("./images/dungeon.png");
	}

	method : public : Load() ~ Bool {
		for(i := 0; i < Clips->DUNGEON_Y_CLIPS; i += 1;) {
			for(j := 0; j < Clips->DUNGEON_X_CLIPS; j += 1;) {
				@dungeon_sprites->AddClip(Rect->New(j * 800, i * 600, 800, 600));
			};
		};

		@dungeon_sprites->SetTop(35);
		@dungeon_sprites->SetLeft(35);
		@dungeon_sprites->SetScale(0.5);

		return true;
	}

	method : public : GetDungeonSprites() ~ AnimatedImageSprite {
		return @dungeon_sprites;
	}

	method : public : Unload() ~ Nil {
		
	}

	consts Clips {
		DUNGEON_X_CLIPS := 4,
		DUNGEON_Y_CLIPS := 6
	}
}

#~
Managers sound assets
~#
class SoundManager from Manager {
	@dungeon_sprites : AnimatedImageSprite;

	New(path : String, framework : GameFramework) {
		Parent();
		@framework := framework;
	}	

	method : public : Load() ~ Bool {
		return false;
	}

	method : public : Unload() ~ Nil {
		
	}
}