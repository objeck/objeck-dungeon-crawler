#~
Tiny Dungeon Crawler
Copyright (c) 2020 Randy Hollines
~#

use System.Concurrency;
use Collection.Generic;
use Game.SDL2;
use Game.Framework;
use System.IO.File;

#~
Manager interface
~#
class Manager {
	method : virtual : public : Load() ~ Bool;
	method : virtual : public : Unload() ~ Nil;
}

#~
Managers map operations
~#
class MapManager from Manager {
	@maps : Vector<MapHolder>;
	@map_index : Int;
	@path : String;
	@player_view : Int[,];
	@player_view_id : Int;

	New(path : String) {
		Parent();

		@path := "{$path}/debug.map";
		@maps := Vector->New()<MapHolder>;
		@player_view := Int->New[5, 3];
		@player_view_id := -1;
	}	
	
	method : public : Load() ~ Bool {
		return LoadMaps();
	}

	method : public : GetStart() ~ Int[] {
		return @maps->Get(@map_index)->GetStart();
	}

	method : GetMap() ~ Byte[,] {
		return @maps->Get(@map_index)->Get();
	}

	method : public : GetView(dir : MapManager->Direction, pos : Int[], print : Bool := false) ~ Int[,] {
		y := pos[0]; x := pos[1];

		# north
		if(dir = MapManager->Direction->NORTH) {
			# first
			@player_view[0,0] := GetType(y, x - 1);
			@player_view[0,1] := GetType(y, x);
			@player_view[0,2] := GetType(y, x + 1);

			# second
			@player_view[1,0] := GetType(y - 1, x - 1);
			@player_view[1,1] := GetType(y - 1, x);
			@player_view[1,2] := GetType(y - 1, x + 1);

			# third
			@player_view[2,0] := GetType(y - 2, x - 1);
			@player_view[2,1] := GetType(y - 2, x);
			@player_view[2,2] := GetType(y - 2, x + 1);

			# forth
			@player_view[3,0] := GetType(y - 3, x - 1);
			@player_view[3,1] := GetType(y - 3, x);
			@player_view[3,2] := GetType(y - 3, x + 1);

			# fifth
			@player_view[4,0] := GetType(y - 4, x - 1);
			@player_view[4,1] := GetType(y - 4, x);
			@player_view[4,2] := GetType(y - 4, x + 1);
		}
		# south
		else if(dir = MapManager->Direction->SOUTH) {
			# first
			@player_view[0,0] := GetType(y, x + 1);
			@player_view[0,1] := GetType(y, x);
			@player_view[0,2] := GetType(y, x - 1);

			# second
			@player_view[1,0] := GetType(y + 1, x + 1);
			@player_view[1,1] := GetType(y + 1, x);
			@player_view[1,2] := GetType(y + 1, x - 1);

			# third
			@player_view[2,0] := GetType(y + 2, x + 1);
			@player_view[2,1] := GetType(y + 2, x);
			@player_view[2,2] := GetType(y + 2, x - 1);

			# forth
			@player_view[3,0] := GetType(y + 3, x + 1);
			@player_view[3,1] := GetType(y + 3, x);
			@player_view[3,2] := GetType(y + 3, x - 1);

			# fifth
			@player_view[4,0] := GetType(y + 4, x + 1);
			@player_view[4,1] := GetType(y + 4, x);
			@player_view[4,2] := GetType(y + 4, x - 1);
		}
		# east
		else if(dir = MapManager->Direction->EAST) {
			# first
			@player_view[0,0] := GetType(y - 1, x);
			@player_view[0,1] := GetType(y, x);
			@player_view[0,2] := GetType(y + 1, x);

			# second
			@player_view[1,0] := GetType(y - 1, x + 1);
			@player_view[1,1] := GetType(y, x + 1);
			@player_view[1,2] := GetType(y + 1, x + 1);

			# third
			@player_view[2,0] := GetType(y - 1 , x + 2);
			@player_view[2,1] := GetType(y, x + 2);
			@player_view[2,2] := GetType(y + 1, x + 2);

			# forth
			@player_view[3,0] := GetType(y - 1, x + 3);
			@player_view[3,1] := GetType(y, x + 3);
			@player_view[3,2] := GetType(y + 1, x + 3);

			# fifth
			@player_view[4,0] := GetType(y - 1, x + 4);
			@player_view[4,1] := GetType(y, x + 4);
			@player_view[4,2] := GetType(y + 1, x + 4);
		}
		# west
		else {
			# first
			@player_view[0,0] := GetType(y + 1, x);
			@player_view[0,1] := GetType(y, x);
			@player_view[0,2] := GetType(y - 1, x);

			# second
			@player_view[1,0] := GetType(y + 1, x - 1);
			@player_view[1,1] := GetType(y, x - 1);
			@player_view[1,2] := GetType(y - 1, x - 1);

			# third
			@player_view[2,0] := GetType(y + 1, x - 2);
			@player_view[2,1] := GetType(y, x - 2);
			@player_view[2,2] := GetType(y - 1 , x - 2);

			# forth
			@player_view[3,0] := GetType(y + 1, x - 3);
			@player_view[3,1] := GetType(y, x - 3);
			@player_view[3,2] := GetType(y - 1, x - 3);

			# fifth
			@player_view[4,0] := GetType(y + 1, x - 4);
			@player_view[4,1] := GetType(y, x - 4);
			@player_view[4,2] := GetType(y - 1, x - 4);
		};

		return @player_view;
	}

	method : public : HashView() ~ Int[] {
		hashes := Int->New[5];

		dims := @player_view->Size();
		for(i := dims[0] - 1; i > -1; i -= 1;) {
			hash_str := "";

			for(j := 0; j < dims[1]; j += 1;) {
				value := @player_view[i,j];
				if(value >= Type->OPEN & value <= Type->END) {
					hash_str += '1';
				}
				else {
					hash_str += '0';
				};
			};
			
			hashes[i] := hash_str->ToInt(2);
		};

		return hashes;
	}

	method : public : SetType(pos : Int[], type : MapManager->Type) ~ Bool {
		y := pos[0]; x := pos[1];
		if(x > -1 & x < Type->MAP_SIZE & y > -1 & y < Type->MAP_SIZE) {
			map := GetMap();
			map[y, x] := type;

			return true;
		};

		return false;
	}

	method : public : GetType(y : Int, x : Int) ~ Int {
		if(x < 0 | x >= Type->MAP_SIZE | y < 0 | y >= Type->MAP_SIZE) {
			return Type->NONE;
		};

		map := GetMap();
		return map[y, x]->As(Int);
	}

	method : LoadMaps() ~ Bool {
		map_file := FileReader->New(@path);
		leaving {
			map_file->Close();
		};

		end_line := map_file->ReadLine();
		while(<>end_line->StartsWith('-')) {
			rows := cols := Type->MAP_SIZE;

			map := Byte->New[rows, cols];
			map_start := Int->New[2];

			for(i := 0; i < rows; i += 1;) {
				line := map_file->ReadLine();
				if(line->Size() <> cols) {
					return false;
				};

				each(j : line) {
					select(line->Get(j)) {
						# clear
						label '@' {
							map[i,j] := MapManager->Type->OPEN;
						}
						# start
						label 'S' {
							map[i,j] := MapManager->Type->START;
							map_start[0] := i; map_start[1] := j;
						}
						# end
						label 'E' {
							map[i,j] := MapManager->Type->END;
						}
						# chest
						label 'L' {
							map[i,j] := MapManager->Type->LOOT;
						}
						# potion
						label 'P' {
							map[i,j] := MapManager->Type->POTION;
						}
						# up latter
						label 'U' {
							map[i,j] := MapManager->Type->LATTER_UP;
						}
						# up down
						label 'D' {
							map[i,j] := MapManager->Type->LATTER_DOWN;
						}
						# trap
						label 'T' {
							map[i,j] := MapManager->Type->TRAP;
						}
					};
				};
			};

			@maps->AddBack(MapHolder->New(map, map_start));
			end_line := map_file->ReadLine();
		};

		return true;
	}

	method : public : Unload() ~ Nil {
	}

	method : public : ShowDebugMap(player_dir : Direction, player_pos : Int[]) ~ Nil {
		map := GetMap();
		map_dimensions := map->Size();

		player_y := player_pos[0];
		player_x := player_pos[1];
		
		for(i := 0; i < map_dimensions[0]; i += 1;) {
			for(j := 0; j < map_dimensions[0]; j += 1;) {
				
				out : Char;
				if(i = player_y & j = player_x) {
					select(player_dir) {
	        			label Direction->NORTH {
	        				out := '↑';
	        			}

	        			label Direction->SOUTH {
	        				out := '↓';
	        			}

	        			label Direction->EAST {
	        				out := '→';
	        			}

	        			label Direction->WEST {
	        				out := '←';
	        			}
	        		};
				}
				else {
					select(map[i,j]) {
						label Type->BLOCKED {
							out := '+';
						}

						label Type->OPEN {
							out := '@';
						}

						label Type->START {
							out := 'S';
						}

						label Type->END {
							out := 'S';
						}

						label Type->POTION {
							out := 'P';
						}
						label Type->LATTER_UP {
							out := 'U';
						}
						# up down
						label Type->LATTER_DOWN {
							out := 'D';
						}

						label Type->LOOT {
							out := 'L';
						}

						label Type->TRAP {
							out := 'T';
						}
					};
				};
				"{$out} "->Print();
			};
			"\n"->Print();
		};
		"--- Player: y={$player_y}, x={$player_x} ---\n"->PrintLine();

		ShowDebugView();
	}

	method : ShowDebugView() ~ Nil {		
		dims := @player_view->Size();
		for(i := dims[0] - 1; i >= 0; i -= 1;) {
			"{$i}| "->Print();
			
			for(j := 0; j < dims[1]; j += 1;) {
				@player_view[i,j]->Print();
				if(j + 1 < dims[1]) {
					','->Print();
				};
			};
			'\n'->Print();
		};
		"--------\n"->PrintLine();
	}

	consts Type {
		MAP_SIZE := 18,
		BLOCKED := 0,
		OPEN := 1,
		START := 2,
		POTION := 3,
		LOOT := 4,
		LATTER_UP := 5,
		LATTER_DOWN := 6,
		END := 7,
		TRAP := 9,
		NONE := 10
	}

	enum Direction {
		NORTH,
		SOUTH,
		EAST,
		WEST
	}

	enum Side {
		LEFT,
		MIDDLE,
		RIGHT
	}

	enum View {
		VIEW_0,
		VIEW_1,
		VIEW_2,
		VIEW_3,
		VIEW_4
	}
}

class MapHolder {
	@map : Byte[,];
	@map_start : Int[];

	New(map : Byte[,], map_start : Int[]) {
		@map := map;
		@map_start := map_start;
	}

	method : public : Get() ~ Byte[,] {	
		return @map;
	}

	method : public : GetStart() ~ Int[] {
		return @map_start;
	}
}

#~
Managers sprite images
~#
class SpriteManager from Manager {
	@framework : GameFramework;
	@dungeon_sprites : AnimatedImageSprite[];
	@map_sprites : AnimatedImageSprite;
	@monster_sprites : AnimatedImageSprite;

	New(path : String, framework : GameFramework) {
		Parent();
		@framework := framework;
		@dungeon_sprites := AnimatedImageSprite->New[SpriteManager->Clips->DUNGEON_SHEETS];
		@dungeon_sprites[0] := @framework->AddAnimatedImageSprite("{$path}/dungeon_0.png");
		@dungeon_sprites[1] := @framework->AddAnimatedImageSprite("{$path}/dungeon_1.png");
		@dungeon_sprites[2] := @framework->AddAnimatedImageSprite("{$path}/dungeon_2.png");
		@dungeon_sprites[3] := @framework->AddAnimatedImageSprite("{$path}/dungeon_3.png");
		@dungeon_sprites[4] := @framework->AddAnimatedImageSprite("{$path}/dungeon_4.png");
		@dungeon_sprites[5] := @framework->AddAnimatedImageSprite("{$path}/dungeon_5.png");

		@map_sprites := @framework->AddAnimatedImageSprite("{$path}/map_0.png");

		@monster_sprites := @framework->AddAnimatedImageSprite("{$path}/monsters_1.png");
	}

	method : public : Load() ~ Bool {
		# dungeon sprites
		for(i := 0; i < Clips->DUNGEON_SHEETS; i += 1;) {
			if(<>@dungeon_sprites[i]->IsOk()) {
				"--- Unable to load images! ---"->ErrorLine();
				return false;
			};

			for(j := 0; j < Clips->DUNGEON_Y_CLIPS; j += 1;) {
				for(k := 0; k < Clips->DUNGEON_X_CLIPS; k += 1;) {
					@dungeon_sprites[i]->AddClip(Rect->New(k * 800, j * 600, 800, 600));
				};
			};
			@dungeon_sprites[i]->SetTop(15);
			@dungeon_sprites[i]->SetLeft(15);
			@dungeon_sprites[i]->SetScale(0.5);
		};

		# map sprites
		if(<>@map_sprites->IsOk()) {
			"--- Unable to load images! ---"->ErrorLine();
			return false;
		};
		@map_sprites->AddClips(Clips->MAP_X_CLIPS, 14, Clips->MAP_Y_CLIPS, 14);

		# monster sprites
		if(<>@monster_sprites->IsOk()) {
			"--- Unable to load images! ---"->ErrorLine();
			return false;
		};		
		@monster_sprites->AddClips(Clips->MONSTER_X_CLIPS, 188, Clips->MONSTER_Y_CLIPS, 224);
		
		return true;
	}

	# -------------

	method : public : RenderMonsterHit() ~ Nil {
		@dungeon_sprites[4]->Render(4);		
	}

	method : public : RenderPlayerHit() ~ Nil {
		@dungeon_sprites[4]->Render(8);		
	}

	method : public : RenderPotion() ~ Nil {
		@dungeon_sprites[5]->Render(3);
	}

	method : public : RenderLoot() ~ Nil {
		@dungeon_sprites[5]->Render(7);
	}



	method : public : RenderDistantDown() ~ Nil {
"--- RenderDistantDown: 19 ---"->PrintLine();
		@dungeon_sprites[5]->Render(19);
	}

	method : public : RenderDown() ~ Nil {
"--- RenderDown ---"->PrintLine();		
		@dungeon_sprites[5]->Render(15);
	}


	method : public : RenderDistantUp() ~ Nil {
"--- RenderDistantUp ---"->PrintLine();		
		@dungeon_sprites[5]->Render(11);
	}

	method : public : RenderUp() ~ Nil {
"--- RenderUp ---"->PrintLine();				
		@dungeon_sprites[5]->Render(23);
	}

	# -------------

	method : public : Render() ~ Nil {
		@dungeon_sprites[5]->Render(0);
	}

	method : public : RenderDungeonSprite(raw_index : Int) ~ Nil {
		if(raw_index > -1) {
			sheet_size := Clips->DUNGEON_Y_CLIPS * Clips->DUNGEON_X_CLIPS;
			sheet_index := raw_index / sheet_size;
			image_index := raw_index - sheet_index * sheet_size;
			# "### raw_index={$raw_index}: sheet_size={$sheet_size}, sheet_index={$sheet_index}, image_index={$image_index} ###"->PrintLine();
			@dungeon_sprites[sheet_index]->Render(image_index);
		};
	}

	method : public : RenderMapSprite(image_index : Int, top : Int, left : Int) ~ Nil {
		if(image_index > -1 & image_index < Clips->MAP_X_CLIPS) {
			@map_sprites->SetTop(top);
			@map_sprites->SetLeft(left);
			@map_sprites->Render(image_index);
		};
	}

	method : public : RenderMonsterSprite(image_index : Int, top : Int, left : Int) ~ Nil {
		if(image_index > -1) {
			@monster_sprites->SetTop(top);
			@monster_sprites->SetLeft(left);
			@monster_sprites->Render(image_index);
		};
	}

	method : public : Unload() ~ Nil {
		
	}

	consts Clips {
		DUNGEON_X_CLIPS := 4,
		DUNGEON_Y_CLIPS := 6,
		DUNGEON_SHEETS := 6,

		MAP_X_CLIPS := 8,
		MAP_Y_CLIPS := 1,
		
		MONSTER_X_CLIPS := 4,
		MONSTER_Y_CLIPS := 3
	}
}

#~
Managers sound assets
~#
class SoundManager from Manager {
	@dungeon_sprites : AnimatedImageSprite;
	@framework : GameFramework;
	
	New(path : String, framework : GameFramework) {
		Parent();
		@framework := framework;
	}	

	method : public : Load() ~ Bool {
		return true;
	}

	method : public : Unload() ~ Nil {
		
	}
}