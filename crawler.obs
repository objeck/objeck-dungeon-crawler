use Game.SDL2;
use Game.Framework;
use System.IO.File;

class Dungeon {
	@framework : GameFramework;

	@dungeon_images : AnimatedImageSprite;
	@dungeon_image_index : Int;

	@monster_images : AnimatedImageSprite;
	@monster_image_index : Int;

	@map_icons : AnimatedImageSprite;
	@map_icon_index : Int;

	@action_icons : AnimatedImageSprite;
	@action_icon_index : Int;

	@map : Char[,];

	@map_start : Int[];
	@map_end : Int[];

	@player_pos : Int[];
	@player_dir : Direction;

	@map_image : ImageSprite;

	@stat_text : TextSprite;
	@stat_color : Color;

	@monster_fight : Bool;
	@hit_monster : Int;
	
	function : Main(args : String[]) ~ Nil {
		Dungeon->New()->Run();
	}

	New() {
		@framework := GameFramework->New(ScreenConsts->SCREEN_WIDTH, ScreenConsts->SCREEN_HEIGHT, "Dungeon");
		@framework->SetClearColor(Color->New(20, 51, 6));
		
		@dungeon_images := @framework->AddAnimatedImageSprite("./media/artwork.png");
		for(i := 0; i < ScreenConsts->DUNGEON_IMAGE_MAX; i += 1;) {
			@dungeon_images->AddClip(Rect->New(i * 410, 0, 410, 310));
		};
		@dungeon_images->SetTop(35);
		@dungeon_images->SetLeft(35);

		@monster_images := @framework->AddAnimatedImageSprite("./media/artwork4.png");
		for(i := 0; i < ScreenConsts->MONSTER_IMAGE_MAX; i += 1;) {
			@monster_images->AddClip(Rect->New(i * 266, 0, 266, 174));
		};
		@monster_images->SetTop(80);
		@monster_images->SetLeft(100);

		@map_icons := @framework->AddAnimatedImageSprite("./media/artwork3.png");
		for(i := 0; i < ScreenConsts->MAP_ICON_MAX; i += 1;) {
			@map_icons->AddClip(Rect->New(i * 27, 0, 27, 27));
		};
		@map_icons->SetTop(10);
		@map_icons->SetLeft(10);

		@action_icons := @framework->AddAnimatedImageSprite("./media/artwork5.png");
		for(i := 0; i < ScreenConsts->ACTIONS_ICON_MAX; i += 1;) {
			@action_icons->AddClip(Rect->New(i * 52, 0, 52, 52));
		};

		@map_image := @framework->AddAnimatedImageSprite("./media/artwork2.png");
		@map_image->SetTop(55);
		@map_image->SetLeft(475);
		
		@stat_text := @framework->AddTextSprite();
		@stat_color := Color->New(249, 249, 249);
		@stat_text->RenderedText("HP: 75, END: 9, STR: 8", @stat_color);
		
		LoadMap();
	}
	
	method : LoadMap() ~ Nil {
		map_path := "./media/debug.dat";
		map_file := FileReader->New(map_path);
		leaving {
			map_file->Close();
		};
		
		size_line := map_file->ReadString();		
		rows := cols := size_line->ToInt();

		@map := Char->New[rows, cols];
		@map_start := Int->New[2];
		@map_end := Int->New[2];

		for(i := 0; i < rows; i += 1;) {
			line := map_file->ReadString();
			if(line->Size() <> cols) {
				"--- Invalid map cols! ---"->ErrorLine();
				Runtime->Exit(1);
			};

			each(j : line) {
				value := line->Get(j);				
				if(value = 'S') {
					@map_start[0] := i;
					@map_start[1] := j;
				}
				else if(value = 'E') {
					@map_end[0] := i;
					@map_end[1] := j;	
				};
				
				@map[i,j] := value;
			};
		};

		@dungeon_image_index := 0;
		@player_pos := @map_start;
		@player_dir := Direction->NORTH;

		UpdateView();
	}

	method : Run() ~ Nil {
		if(@framework->IsOk()) {
			e := @framework->GetEvent();
			
			frame_count := 0;
			quit := false;
			while(<>quit) {
				@framework->FrameStart();
				
				# process input
				while(e->Poll() <> 0) {
					# quit
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					}
					# keyboard
					else if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
				        GetInput(e);
				    };
				};

				Render(frame_count);

				@framework->FrameEnd();

				frame_count += 1;
				if(frame_count >= @framework->GetFps()) {
					frame_count := 0;
				};
			};
		}
		else {
			"--- Error Initializing Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@framework->Quit();
		};
	}

	method : GetInput(e : Event) ~ Nil {
        select(e->GetKey()->GetKeysym()->GetScancode()) {
        	label Scancode->SDL_SCANCODE_UP:
        	label Scancode->SDL_SCANCODE_W: {
        		if(<>@monster_fight) {
	        		if(MoveForwad() & Int->Random(3) = 0) {
	        			@monster_fight := true;
	        			StartFight();
	        		}
	        		else {
	        			@monster_fight := false;
	        		};

	        		UpdateView();
	        	};
        	}

        	label Scancode->SDL_SCANCODE_LEFT:
        	label Scancode->SDL_SCANCODE_A: {
        		if(<>@monster_fight) {
	        		select(@player_dir) {
	        			label Direction->NORTH {
	        				@player_dir := Direction->WEST;
	        			}

	        			label Direction->SOUTH {
	        				@player_dir := Direction->EAST;
	        			}

	        			label Direction->EAST {
	        				@player_dir := Direction->NORTH;
	        			}

	        			label Direction->WEST {
	        				@player_dir := Direction->SOUTH;
	        			}
	        		};

	        		UpdateView();
	        	};
        	}

        	label Scancode->SDL_SCANCODE_RIGHT:
        	label Scancode->SDL_SCANCODE_D: {
        		if(<>@monster_fight) {
	 				select(@player_dir) {
	        			label Direction->NORTH {
	        				@player_dir := Direction->EAST;
	        			}

	        			label Direction->SOUTH {
	        				@player_dir := Direction->WEST;
	        				
	        			}

	        			label Direction->EAST {
	        				@player_dir := Direction->SOUTH;
	        			}

	        			label Direction->WEST {
	        				@player_dir := Direction->NORTH;
	        			}
	        		};

	        		UpdateView();
	        	};
        	}

        	label Scancode->SDL_SCANCODE_SPACE: {
        		if(@monster_fight) {
"--- Sword ---"->PrintLine();
					@hit_monster := 10;
        			Fight(FightType->SWORD);
        		};
        	}

        	label Scancode->SDL_SCANCODE_P: {
"--- Potion ---"->PrintLine();
        	}

        	label Scancode->SDL_SCANCODE_S: {
        		if(@monster_fight) {
"--- Spell ---"->PrintLine();
        			Fight(FightType->SPELL);
        		};
        	}
        };
	}

	# -------

	method : StartFight() ~ Nil {

	}

	method : Fight(type : FightType) ~ Nil {

	}

	# -------

	method : Render(frame_count : Int) ~ Nil {
		@framework->Clear();

		@map_image->Render();
		RenderCave(frame_count);		
		@stat_text->Render(500, 35);
		RenderMap(frame_count);

		@framework->Show();
	}

	method : RenderCave(frame_count : Int) ~ Nil {
		if(@hit_monster > 0) {
			@dungeon_images->Render(ScreenConsts->DUNGEON_IMAGE_MAX - 1);
		}
		else {
			@dungeon_images->Render(@dungeon_image_index);
		};

		@action_icons->SetTop(350);
		@action_icons->SetLeft(165);
		if(@hit_monster > 0) {
	    	@action_icons->Render(3);
	    }
	    else {
	    	@action_icons->Render(0);
	    };

	    @action_icons->SetTop(350);
		@action_icons->SetLeft(235);
#		if(@hit_monster > 0) {
#	    	@action_icons->Render(3);
#	    }
#	    else {
	    	@action_icons->Render(1);
#	    };

	    @action_icons->SetTop(350);
		@action_icons->SetLeft(300);
#		if(@hit_monster > 0) {
#	    	@action_icons->Render(3);
#	    }
#	    else {
	    	@action_icons->Render(2);
#	    };

		if(@monster_fight) {
			if(frame_count % 30 = 0) {
				@monster_image_index += 1;
				if(@monster_image_index >= ScreenConsts->MONSTER_IMAGE_MAX) {
					@monster_image_index := 0;
				};
			};
			@monster_images->Render(@monster_image_index);

			@action_icons->SetTop(50);
			@action_icons->SetLeft(60);
			@action_icons->Render(4);
		};

		if(@hit_monster > 0) {
			@hit_monster -= 1;
		};
	}

	method : RenderMap(frame_count : Int) ~ Nil {
		top := 77;
		left := 499;
		inc := 29;

		map_dimensions := @map->Size();

		player_x := @player_pos[1];
		player_y := @player_pos[0];

		for(i := 0; i < map_dimensions[0]; i += 1;) {
			for(j := 0; j < map_dimensions[0]; j += 1;) {
				value := @map[i,j];

				@map_icons->SetTop(top);
				@map_icons->SetLeft(left);

				if(i = player_y & j = player_x) {
					select(@player_dir) {
	        			label Direction->NORTH {
	        				@map_icons->SetAngle(0);
	        				@map_icons->Render(1);
	        			}

						label Direction->EAST {
	        				@map_icons->SetAngle(90);
	        				@map_icons->Render(1);
	        			}
	        			label Direction->SOUTH {
	        				@map_icons->SetAngle(180);
	        				@map_icons->Render(1);
	        			}

	        			label Direction->WEST {
	        				@map_icons->SetAngle(270);
	        				@map_icons->Render(1);
	        			}
	        		};
				}
				else if(value = 'X') {					
					@map_icons->Render(0);
				};
				left += inc;
			};
			top += inc;
			left := 499;
		};
	}

	method : MoveForwad() ~ Bool {
		map_dimensions := @map->Size();

		map_dimension_x := map_dimensions[1];
		map_dimension_y := map_dimensions[0];

		player_x := @player_pos[1];
		player_y := @player_pos[0];

		# mark title discovered
		if(@map[player_y, player_x] = '@' | @map[player_y, player_x] = 'S') {
			@map[player_y, player_x] := 'X';
		};

		if(Direction->NORTH = @player_dir & player_y - 1 > -1 & 
				CheckViewPosition(player_y - 1, player_x)) {
			@player_pos[0] := player_y - 1;
			return true;
		}
		else if(Direction->EAST = @player_dir & player_x + 1 < map_dimension_x & 
				CheckViewPosition(player_y, player_x + 1)) {
			@player_pos[1] := player_x + 1;
			return true;
		}
		else if(Direction->SOUTH = @player_dir & player_y + 1 < map_dimension_y & 
				CheckViewPosition(player_y + 1, player_x)) {
			@player_pos[0] := player_y + 1;
			return true;
		}
		else if(Direction->WEST = @player_dir & player_x - 1 > -1 & 
				CheckViewPosition(player_y, player_x - 1)) {
			@player_pos[1] := player_x - 1;
			return true;
		};

		return false;
	}

	method : CheckViewPosition(player_y : Int, player_x : Int) ~ Bool {
		map_dimensions := @map->Size();

		map_dimension_x := map_dimensions[1];
		map_dimension_y := map_dimensions[0];

		if(player_y < 0 | player_y >= map_dimension_y) {
			return false;
		};

		if(player_x < 0 | player_x >= map_dimension_x) {
			return false;
		};

		if(@map[player_y, player_x] = 'X' | @map[player_y, player_x] = '@' | @map[player_y, player_x] = 'S') {
			return true;
		}
		else {
			return false;
		};
	}

	method : UpdateView() ~ Nil {
		player_x := @player_pos[1];
		player_y := @player_pos[0];

		is_forward_2 : Bool; is_forward_1 : Bool; is_forward_0 : Bool;
		is_left_1 : Bool; is_left_0 : Bool;
		is_right_1 : Bool; is_right_0 : Bool;
		
		if(Direction->NORTH = @player_dir) {
# "NORTH"->PrintLine();		
			# foward
			is_forward_2 := CheckViewPosition(player_y - 2, player_x) & CheckViewPosition(player_y - 1, player_x);
			is_forward_1 := CheckViewPosition(player_y - 1, player_x);
			is_forward_0 := <>is_forward_2 & <>is_forward_1 & CheckViewPosition(player_y, player_x);
			# left
			is_left_1 := is_forward_1 & CheckViewPosition(player_y - 1, player_x - 1);
			is_left_0 := CheckViewPosition(player_y, player_x - 1);
			# right
			is_right_1 := is_forward_1 & CheckViewPosition(player_y - 1, player_x + 1);
			is_right_0 := CheckViewPosition(player_y, player_x + 1);
		}
		else if(Direction->EAST = @player_dir) {
# "EAST"->PrintLine();			
			# foward
			is_forward_2 := CheckViewPosition(player_y, player_x + 2) & CheckViewPosition(player_y, player_x + 1);
			is_forward_1 := CheckViewPosition(player_y, player_x + 1);
			is_forward_0 := <>is_forward_2 & <>is_forward_1 & CheckViewPosition(player_y, player_x);
			# left
			is_left_1 := is_forward_1 & CheckViewPosition(player_y - 1, player_x + 1);
			is_left_0 := CheckViewPosition(player_y - 1, player_x);
			# right
			is_right_1 := is_forward_1 & CheckViewPosition(player_y + 1, player_x + 1);
			is_right_0 := CheckViewPosition(player_y + 1, player_x);
		}
		else if(Direction->SOUTH = @player_dir) {
# "SOUTH"->PrintLine();
			# foward
			is_forward_2 := CheckViewPosition(player_y + 2, player_x) & CheckViewPosition(player_y + 1, player_x);
			is_forward_1 := CheckViewPosition(player_y + 1, player_x);
			is_forward_0 := <>is_forward_2 & <>is_forward_1 & CheckViewPosition(player_y, player_x);
			# left
			is_left_1 := is_forward_1 & CheckViewPosition(player_y + 1, player_x + 1);
			is_left_0 := CheckViewPosition(player_y, player_x + 1);
			# right
			is_right_1 := is_forward_1 & CheckViewPosition(player_y + 1, player_x - 1);
			is_right_0 := CheckViewPosition(player_y, player_x - 1);	
		}
		else if(Direction->WEST = @player_dir) {
# "WEST"->PrintLine();
			# foward
			is_forward_2 := CheckViewPosition(player_y, player_x - 2) & CheckViewPosition(player_y, player_x - 1);
			is_forward_1 := CheckViewPosition(player_y, player_x - 1);
			is_forward_0 := <>is_forward_2 & <>is_forward_1 & CheckViewPosition(player_y, player_x);
			# left
			is_left_1 := is_forward_1 & CheckViewPosition(player_y + 1, player_x - 1);
			is_left_0 := CheckViewPosition(player_y + 1, player_x);
			# right
			is_right_1 := is_forward_1 & CheckViewPosition(player_y - 1, player_x - 1);
			is_right_0 := CheckViewPosition(player_y - 1, player_x);	
		};

#~
"  fwd_2={$is_forward_2}, fwd_1={$is_forward_1}, fwd_0={$is_forward_0}"->PrintLine();
"  left_1={$is_left_1}, right_1={$is_right_1}"->PrintLine();
"  left_0={$is_left_0}, right_0={$is_right_0}"->PrintLine();
~#

		#
		# common logic
		#

		# foward 2
		if(is_forward_2 & is_left_1 & is_right_1) {
			@dungeon_image_index := 8;
		}
		else if(is_forward_2 & is_left_1) {
			@dungeon_image_index := 9;
		}
		else if(is_forward_2 & is_right_1) {
			@dungeon_image_index := 10;
		}
		# foward 1
		else if(is_forward_1 & is_left_1 & is_right_1) {
			@dungeon_image_index := 4;
		}
		else if(is_forward_1 & is_left_1) {
			@dungeon_image_index := 5;
		}
		else if(is_forward_1 & is_right_1) {
			@dungeon_image_index := 6;
		}
		# foward 0
		else if(is_forward_0 & is_left_0 & is_right_0) {
			@dungeon_image_index := 0;
		}
		else if(is_forward_0 & is_left_0) {
			@dungeon_image_index := 1;
		}
		else if(is_forward_0 & is_right_0) {
			@dungeon_image_index := 2;
		}
		# tunnel
		else if(is_forward_2) {
			@dungeon_image_index := 11;	
		}
		else if(is_forward_1) {
			@dungeon_image_index := 7;		
		}
		else {
			@dungeon_image_index := 3;
		};

#		ShowMap();
	}

	method : ShowMap() ~ Nil {
		map_dimensions := @map->Size();

		player_x := @player_pos[1];
		player_y := @player_pos[0];

		for(i := 0; i < map_dimensions[0]; i += 1;) {
			for(j := 0; j < map_dimensions[0]; j += 1;) {
				value := @map[i,j];
				if(i = player_y & j = player_x) {
					select(@player_dir) {
	        			label Direction->NORTH {
	        				value := '↑';
	        			}

	        			label Direction->SOUTH {
	        				value := '↓';
	        			}

	        			label Direction->EAST {
	        				value := '→';
	        			}

	        			label Direction->WEST {
	        				value := '←';
	        			}
	        		};
				};
				"{$value} "->Print();
			};
			"\n"->Print();
		};
		"--- {$player_x},{$player_y} ---\n"->PrintLine();
	}
}

class Player {
	@hp : Int;
	@attack : Int;
	@defence : Int;

	New() {
		@hp := Int->Random(7, 10);
		@attack := Int->Random(3, 5);
		@defence := Int->Random(2, 4);
	}
}

class Monster {
	@type : MonsterType;
	@hp : Int;
	@attack : Int;
	@defence : Int;

	New(type : MonsterType) {
		@type := type;
		@hp := Int->Random(3, 5);
		@attack := Int->Random(2, 3);
		@defence := Int->Random(1, 3);
	}


}

consts ScreenConsts {
	SCREEN_WIDTH := 800,
	SCREEN_HEIGHT := 420,
	DUNGEON_IMAGE_MAX := 14,
	MONSTER_IMAGE_MAX := 3,
	MAP_ICON_MAX := 2,
	ACTIONS_ICON_MAX := 7
}

enum FightType {
	SWORD,
	SPELL
}

enum MonsterType {
	SLIME,
	RAT
}

enum Direction {
	NORTH,
	SOUTH,
	EAST,
	WEST
}