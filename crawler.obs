use Game.SDL2;
use Game.Framework;
use System.IO.File;

class Dungeon {
	@framework : GameFramework;

	@tunnel_imgs : AnimatedImageSprite;
	@tunnel_index : Int;
	@tunnel_image_max : Int;

	@map : Char[,];

	@map_start : Int[];
	@map_end : Int[];

	@player_pos : Int[];
	@player_dir : Int;
	
	function : Main(args : String[]) ~ Nil {
		Dungeon->New()->Run();
	}

	New() {
		@framework := GameFramework->New(ScreenConsts->SCREEN_WIDTH, ScreenConsts->SCREEN_HEIGHT, "Dungeon");
		@framework->SetClearColor(Color->New(0, 0, 0));
		
		tunnel_path := "./media/images.png";
		@tunnel_image_max := ScreenConsts->IMAGE_MAX;
		@tunnel_imgs := @framework->AddAnimatedImageSprite(tunnel_path);	
		for(i := 0; i < @tunnel_image_max; i += 1;) {
			@tunnel_imgs->AddClip(Rect->New(i * 400, 0, 400, 300));
		};
		@tunnel_imgs->SetTop(16);
		@tunnel_imgs->SetLeft(16);

		LoadMap();

		@player_pos := @map_start;
		@player_dir := Direction->NORTH;
	}
	
	method : LoadMap() ~ Nil {
		map_path := "./media/debug.dat";
		map_file := FileReader->New(map_path);
		leaving {
			map_file->Close();
		};
		
		size_line := map_file->ReadString();		
		rows := cols := size_line->ToInt();

		@map := Char->New[rows, cols];
		@map_start := Int->New[2];
		@map_end := Int->New[2];

		for(i := 0; i < rows; i += 1;) {
			line := map_file->ReadString();
			if(line->Size() <> cols) {
				"--- Invalid map cols! ---"->ErrorLine();
				Runtime->Exit(1);
			};

			each(j : line) {
				value := line->Get(j);				
				if(value = 'S') {
					@map_start[0] := i;
					@map_start[1] := j;
				}
				else if(value = 'E') {
					@map_end[0] := i;
					@map_end[1] := j;	
				};
				
				@map[i, j] := value;
			};
		};

		@tunnel_index := 0;
	}

	method : Run() ~ Nil {
		if(@framework->IsOk()) {
			e := @framework->GetEvent();
			
			frame_count := 0;
			quit := false;
			while(<>quit) {
				@framework->FrameStart();
				
				# process input
				while(e->Poll() <> 0) {
					# quit
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					}
					# keyboard
					else if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
				        GetInput(e);
				    };
				};

				Render(frame_count);

				@framework->FrameEnd();

				frame_count += 1;
				if(frame_count >= @framework->GetFps()) {
					frame_count := 0;
				};
			};
		}
		else {
			"--- Error Initializing Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@framework->Quit();
		};
	}

	method : GetInput(e : Event) ~ Nil {
        select(e->GetKey()->GetKeysym()->GetScancode()) {
        	label Scancode->SDL_SCANCODE_UP:
        	label Scancode->SDL_SCANCODE_W: {
        		MoveForwad();
        	}

        	label Scancode->SDL_SCANCODE_LEFT:
        	label Scancode->SDL_SCANCODE_A: {
        		select(@player_dir) {
        			label Direction->NORTH {
        				@player_dir := Direction->WEST;
        			}

        			label Direction->SOUTH {
        				@player_dir := Direction->EAST;
        			}

        			label Direction->EAST {
        				@player_dir := Direction->NORTH;
        			}

        			label Direction->WEST {
        				@player_dir := Direction->SOUTH;
        			}
        		};
        	}

        	label Scancode->SDL_SCANCODE_RIGHT:
        	label Scancode->SDL_SCANCODE_D: {
 				select(@player_dir) {
        			label Direction->NORTH {
        				@player_dir := Direction->EAST;
        			}

        			label Direction->SOUTH {
        				@player_dir := Direction->WEST;
        				
        			}

        			label Direction->EAST {
        				@player_dir := Direction->SOUTH;
        			}

        			label Direction->WEST {
        				@player_dir := Direction->NORTH;
        			}
        		};	
        	}
        };
	}

	method : MoveForwad() ~ Nil {
		map_dimensions := @map->Size();		
		map_dimension_x := map_dimensions[1];
		map_dimension_y := map_dimensions[0];

		player_x := @player_pos[1];
		player_y := @player_pos[0];

		# check if we can move forward
		if(player_y - 1 < map_dimension_y & @map[player_y - 1, player_x] = '@') {
			@player_pos[0] := --player_y;
			UpdateView(map_dimension_x, map_dimension_y);			
		};

value := @map[player_y, player_x];
"pos: x={$player_x}, y={$player_y}, value='{$value}'\n======"->PrintLine();
	}

	method : UpdateView(map_dimension_x : Int, map_dimension_y : Int) ~ Nil {
		player_x := @player_pos[1];
		player_y := @player_pos[0];

		ShowMap();
	}

	method : Render(frame_count : Int) ~ Nil {
		@framework->Clear();

		@tunnel_imgs->Render(@tunnel_index);

		@framework->Show();
	}

	method : ShowMap() ~ Nil {
		map_dimensions := @map->Size();

		player_x := @player_pos[1];
		player_y := @player_pos[0];

		for(i := 0; i < map_dimensions[0]; i += 1;) {
			for(j := 0; j < map_dimensions[0]; j += 1;) {
				value := @map[i,j];
				if(i = player_y & j = player_x) {
					value := '^';
				};
				"{$value} "->Print();
			};
			"\n"->Print();
		};
	}
}

consts ScreenConsts {
	SCREEN_WIDTH := 800,
	SCREEN_HEIGHT := 600,
	IMAGE_MAX := 7
}

enum Direction {
	NORTH,
	SOUTH,
	EAST,
	WEST
}