use Game.SDL2;
use Game.Framework;
use System.IO.File;

class Dungeon {
	@framework : GameFramework;

	@dungeon_images : AnimatedImageSprite;
	@dungeon_image_index : Int;

	@monster_images : AnimatedImageSprite;
	@monster_image_index : Int;

	@map_icons : AnimatedImageSprite;
	@map_icon_index : Int;

	@action_icons : AnimatedImageSprite;
	@action_icon_index : Int;

	@map : Char[,];

	@map_start : Int[];
	@map_end : Int[];

	@player_pos : Int[];
	@player_dir : Direction;

	@map_image : ImageSprite;

	@stat_text : TextSprite;
	@stat_color : Color;

	@player : Player;
	@player_percent : Float;

	@monster : Entity;
	@monster_percent : Float;
	
	@hit_view_timer : Int;
		
	function : Main(args : String[]) ~ Nil {
		Dungeon->New()->Run();
	}

	New() {
		@player := Player->New();

		@framework := GameFramework->New(ScreenConsts->SCREEN_WIDTH, ScreenConsts->SCREEN_HEIGHT, "Dungeon");
		@framework->SetClearColor(Color->New(20, 51, 6));
		
		@dungeon_images := @framework->AddAnimatedImageSprite("./media/artwork.png");
		for(i := 0; i < ScreenConsts->DUNGEON_IMAGE_MAX; i += 1;) {
			@dungeon_images->AddClip(Rect->New(i * 410, 0, 410, 310));
		};
		@dungeon_images->SetTop(35);
		@dungeon_images->SetLeft(35);

		@monster_images := @framework->AddAnimatedImageSprite("./media/artwork4.png");
		for(i := 0; i < ScreenConsts->MONSTER_IMAGE_MAX; i += 1;) {
			@monster_images->AddClip(Rect->New(i * 266, 0, 266, 174));
		};
		@monster_images->SetTop(80);
		@monster_images->SetLeft(100);

		@map_icons := @framework->AddAnimatedImageSprite("./media/artwork3.png");
		for(i := 0; i < ScreenConsts->MAP_ICON_MAX; i += 1;) {
			@map_icons->AddClip(Rect->New(i * 27, 0, 27, 27));
		};
		@map_icons->SetTop(10);
		@map_icons->SetLeft(10);

		@action_icons := @framework->AddAnimatedImageSprite("./media/artwork5.png");
		for(i := 0; i < ScreenConsts->ACTIONS_ICON_MAX; i += 1;) {
			@action_icons->AddClip(Rect->New(i * 52, 0, 52, 52));
		};

		@map_image := @framework->AddAnimatedImageSprite("./media/artwork2.png");
		@map_image->SetTop(55);
		@map_image->SetLeft(475);
		
		@stat_text := @framework->AddTextSprite();
		@stat_color := Color->New(249, 249, 249);

		hp := @player->GetHp();
		attack := @player->GetAttack();
		defense := @player->GetDefense();
		@stat_text->RenderedText("HP={$hp}, ATK={$attack}, DEF={$attack}", @stat_color);
		
		LoadMap();
	}
	
	# --- Game Loop ---

	method : Run() ~ Nil {
		if(@framework->IsOk()) {
			e := @framework->GetEvent();
			
			frame_count := 0;
			quit := false;
			while(<>quit) {
				@framework->FrameStart();
				
				# process input
				while(e->Poll() <> 0) {
					# quit
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					}
					# keyboard
					else if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
				        GetInput(e);
				    };
				};

				Render(frame_count);

				@framework->FrameEnd();

				frame_count += 1;
				if(frame_count >= @framework->GetFps()) {
					frame_count := 0;
				};
			};
		}
		else {
			"--- Error Initializing Environment ---"->ErrorLine();
			return;
		};

		leaving {
			@framework->Quit();
		};
	}

	# --- Input Logic ---

	method : GetInput(e : Event) ~ Nil {
        select(e->GetKey()->GetKeysym()->GetScancode()) {
        	label Scancode->SDL_SCANCODE_UP:
        	label Scancode->SDL_SCANCODE_W: {
        		if(<>@monster <> Nil) {
	        		if(MoveForwad() & Int->Random(3) = 0) {
	        			StartFight();
	        		}
	        		else {
	        			@monster := Nil;
	        		};

	        		UpdateDungeonView();
	        	};
        	}

        	label Scancode->SDL_SCANCODE_LEFT:
        	label Scancode->SDL_SCANCODE_A: {
        		if(@monster = Nil) {
	        		select(@player_dir) {
	        			label Direction->NORTH {
	        				@player_dir := Direction->WEST;
	        			}

	        			label Direction->SOUTH {
	        				@player_dir := Direction->EAST;
	        			}

	        			label Direction->EAST {
	        				@player_dir := Direction->NORTH;
	        			}

	        			label Direction->WEST {
	        				@player_dir := Direction->SOUTH;
	        			}
	        		};

	        		UpdateDungeonView();
	        	};
        	}

        	label Scancode->SDL_SCANCODE_RIGHT:
        	label Scancode->SDL_SCANCODE_D: {
        		if(@monster = Nil) {
	 				select(@player_dir) {
	        			label Direction->NORTH {
	        				@player_dir := Direction->EAST;
	        			}

	        			label Direction->SOUTH {
	        				@player_dir := Direction->WEST;
	        				
	        			}

	        			label Direction->EAST {
	        				@player_dir := Direction->SOUTH;
	        			}

	        			label Direction->WEST {
	        				@player_dir := Direction->NORTH;
	        			}
	        		};

	        		UpdateDungeonView();
	        	};
        	}

        	label Scancode->SDL_SCANCODE_SPACE: {
        		if(@monster <> Nil) {
"--- Sword ---"->PrintLine();
        			Fight(AttackType->SWORD);
        		};
        	}

        	label Scancode->SDL_SCANCODE_R: {
"--- Rest ---"->PrintLine();
				if(@monster = Nil) {
					@player->Rest();
				};
        	}

        	label Scancode->SDL_SCANCODE_P: {
"--- Potion ---"->PrintLine();
        	}

        	label Scancode->SDL_SCANCODE_S: {
        		if(@monster <> Nil) {
"--- Spell ---"->PrintLine();
        		};
        	}
        };
	}

	# --- Render Logic ---

	method : Render(frame_count : Int) ~ Nil {
		@framework->Clear();

		@map_image->Render();
		RenderCave(frame_count);		
		RenderStats(frame_count);
		RenderMap(frame_count);

		@framework->Show();
	}

	method : RenderStats(frame_count : Int) ~ Nil {
		hp := @player->GetHp();
		attack := @player->GetAttack();
		defense := @player->GetDefense();
		@stat_text->RenderedText("HP={$hp}, ATK={$attack}, DEF={$attack}", @stat_color);
		@stat_text->Render(500, 35);
	}

	method : RenderCave(frame_count : Int) ~ Nil {
		if(@hit_view_timer > 0) {
			@dungeon_images->Render(ScreenConsts->DUNGEON_IMAGE_MAX - 1);
		}
		else {
			@dungeon_images->Render(@dungeon_image_index);
		};

		# TOOD: update button logic
		@action_icons->SetTop(350);
		@action_icons->SetLeft(105);
		if(@hit_view_timer > 0) {
	    	@action_icons->Render(4);
	    }
	    else {
	    	@action_icons->Render(0);
	    };

	    @action_icons->SetTop(350);
		@action_icons->SetLeft(175);
#		if(@hit_view_timer > 0) {
#	    	@action_icons->Render(4);
#	    }
#	    else {
	    	@action_icons->Render(1);
#	    };

	    @action_icons->SetTop(350);
		@action_icons->SetLeft(240);
#		if(@hit_view_timer > 0) {
#	    	@action_icons->Render(4);
#	    }
#	    else {
	    	@action_icons->Render(2);
#	    };

		@action_icons->SetTop(350);
		@action_icons->SetLeft(305);
#		if(@hit_view_timer > 0) {
#	    	@action_icons->Render(4);
#	    }
#	    else {
	    	@action_icons->Render(3);
#	    };

		if(@monster <> Nil) {
			if(frame_count % 30 = 0) {
				@monster_image_index += 1;
				if(@monster_image_index >= ScreenConsts->MONSTER_IMAGE_MAX) {
					@monster_image_index := 0;
				};
			};
			@monster_images->Render(@monster_image_index);

			@action_icons->SetTop(50);
			@action_icons->SetLeft(60);
			if(@monster_percent > 0.75) {
				@action_icons->Render(5);
			}
			else if(@monster_percent <= 0.75 & @monster_percent > 0.50) {
				@action_icons->Render(6);
			}
			else if(@monster_percent <= 0.50 & @monster_percent > 0.25) {
				@action_icons->Render(7);
			}
			else {
				@action_icons->Render(8);
			};
		};

		if(@hit_view_timer > 0) {
			@hit_view_timer -= 1;
		};
	}

	method : RenderMap(frame_count : Int) ~ Nil {
		top := 77;
		left := 499;
		inc := 29;

		map_dimensions := @map->Size();

		player_x := @player_pos[1];
		player_y := @player_pos[0];

		for(i := 0; i < map_dimensions[0]; i += 1;) {
			for(j := 0; j < map_dimensions[0]; j += 1;) {
				value := @map[i,j];

				@map_icons->SetTop(top);
				@map_icons->SetLeft(left);

				if(i = player_y & j = player_x) {
					select(@player_dir) {
	        			label Direction->NORTH {
	        				@map_icons->SetAngle(0);
	        				@map_icons->Render(1);
	        			}

						label Direction->EAST {
	        				@map_icons->SetAngle(90);
	        				@map_icons->Render(1);
	        			}
	        			label Direction->SOUTH {
	        				@map_icons->SetAngle(180);
	        				@map_icons->Render(1);
	        			}

	        			label Direction->WEST {
	        				@map_icons->SetAngle(270);
	        				@map_icons->Render(1);
	        			}
	        		};
				}
				else if(value = 'X') {					
					@map_icons->Render(0);
				};
				left += inc;
			};
			top += inc;
			left := 499;
		};
	}

	# --- Player Movement Logic ---

	method : MoveForwad() ~ Bool {
		map_dimensions := @map->Size();

		map_dimension_x := map_dimensions[1];
		map_dimension_y := map_dimensions[0];

		player_x := @player_pos[1];
		player_y := @player_pos[0];

		# mark title discovered
		if(@map[player_y, player_x] = '@' | @map[player_y, player_x] = 'S') {
			@map[player_y, player_x] := 'X';
		};

		if(Direction->NORTH = @player_dir & player_y - 1 > -1 & 
				CheckPlayerPosition(player_y - 1, player_x)) {
			@player_pos[0] := player_y - 1;
			return true;
		}
		else if(Direction->EAST = @player_dir & player_x + 1 < map_dimension_x & 
				CheckPlayerPosition(player_y, player_x + 1)) {
			@player_pos[1] := player_x + 1;
			return true;
		}
		else if(Direction->SOUTH = @player_dir & player_y + 1 < map_dimension_y & 
				CheckPlayerPosition(player_y + 1, player_x)) {
			@player_pos[0] := player_y + 1;
			return true;
		}
		else if(Direction->WEST = @player_dir & player_x - 1 > -1 & 
				CheckPlayerPosition(player_y, player_x - 1)) {
			@player_pos[1] := player_x - 1;
			return true;
		};

		return false;
	}

	method : UpdateDungeonView() ~ Nil {
		player_x := @player_pos[1];
		player_y := @player_pos[0];

		is_forward_2 : Bool; is_forward_1 : Bool; is_forward_0 : Bool;
		is_left_1 : Bool; is_left_0 : Bool;
		is_right_1 : Bool; is_right_0 : Bool;
		
		if(Direction->NORTH = @player_dir) {
# "NORTH"->PrintLine();		
			# foward
			is_forward_2 := CheckPlayerPosition(player_y - 2, player_x) & CheckPlayerPosition(player_y - 1, player_x);
			is_forward_1 := CheckPlayerPosition(player_y - 1, player_x);
			is_forward_0 := <>is_forward_2 & <>is_forward_1 & CheckPlayerPosition(player_y, player_x);
			# left
			is_left_1 := is_forward_1 & CheckPlayerPosition(player_y - 1, player_x - 1);
			is_left_0 := CheckPlayerPosition(player_y, player_x - 1);
			# right
			is_right_1 := is_forward_1 & CheckPlayerPosition(player_y - 1, player_x + 1);
			is_right_0 := CheckPlayerPosition(player_y, player_x + 1);
		}
		else if(Direction->EAST = @player_dir) {
# "EAST"->PrintLine();			
			# foward
			is_forward_2 := CheckPlayerPosition(player_y, player_x + 2) & CheckPlayerPosition(player_y, player_x + 1);
			is_forward_1 := CheckPlayerPosition(player_y, player_x + 1);
			is_forward_0 := <>is_forward_2 & <>is_forward_1 & CheckPlayerPosition(player_y, player_x);
			# left
			is_left_1 := is_forward_1 & CheckPlayerPosition(player_y - 1, player_x + 1);
			is_left_0 := CheckPlayerPosition(player_y - 1, player_x);
			# right
			is_right_1 := is_forward_1 & CheckPlayerPosition(player_y + 1, player_x + 1);
			is_right_0 := CheckPlayerPosition(player_y + 1, player_x);
		}
		else if(Direction->SOUTH = @player_dir) {
# "SOUTH"->PrintLine();
			# foward
			is_forward_2 := CheckPlayerPosition(player_y + 2, player_x) & CheckPlayerPosition(player_y + 1, player_x);
			is_forward_1 := CheckPlayerPosition(player_y + 1, player_x);
			is_forward_0 := <>is_forward_2 & <>is_forward_1 & CheckPlayerPosition(player_y, player_x);
			# left
			is_left_1 := is_forward_1 & CheckPlayerPosition(player_y + 1, player_x + 1);
			is_left_0 := CheckPlayerPosition(player_y, player_x + 1);
			# right
			is_right_1 := is_forward_1 & CheckPlayerPosition(player_y + 1, player_x - 1);
			is_right_0 := CheckPlayerPosition(player_y, player_x - 1);	
		}
		else if(Direction->WEST = @player_dir) {
# "WEST"->PrintLine();
			# foward
			is_forward_2 := CheckPlayerPosition(player_y, player_x - 2) & CheckPlayerPosition(player_y, player_x - 1);
			is_forward_1 := CheckPlayerPosition(player_y, player_x - 1);
			is_forward_0 := <>is_forward_2 & <>is_forward_1 & CheckPlayerPosition(player_y, player_x);
			# left
			is_left_1 := is_forward_1 & CheckPlayerPosition(player_y + 1, player_x - 1);
			is_left_0 := CheckPlayerPosition(player_y + 1, player_x);
			# right
			is_right_1 := is_forward_1 & CheckPlayerPosition(player_y - 1, player_x - 1);
			is_right_0 := CheckPlayerPosition(player_y - 1, player_x);	
		};

#~
"  fwd_2={$is_forward_2}, fwd_1={$is_forward_1}, fwd_0={$is_forward_0}"->PrintLine();
"  left_1={$is_left_1}, right_1={$is_right_1}"->PrintLine();
"  left_0={$is_left_0}, right_0={$is_right_0}"->PrintLine();
~#

		#
		# common logic
		#

		# foward 2
		if(is_forward_2 & is_left_1 & is_right_1) {
			@dungeon_image_index := 8;
		}
		else if(is_forward_2 & is_left_1) {
			@dungeon_image_index := 9;
		}
		else if(is_forward_2 & is_right_1) {
			@dungeon_image_index := 10;
		}
		# foward 1
		else if(is_forward_1 & is_left_1 & is_right_1) {
			@dungeon_image_index := 4;
		}
		else if(is_forward_1 & is_left_1) {
			@dungeon_image_index := 5;
		}
		else if(is_forward_1 & is_right_1) {
			@dungeon_image_index := 6;
		}
		# foward 0
		else if(is_forward_0 & is_left_0 & is_right_0) {
			@dungeon_image_index := 0;
		}
		else if(is_forward_0 & is_left_0) {
			@dungeon_image_index := 1;
		}
		else if(is_forward_0 & is_right_0) {
			@dungeon_image_index := 2;
		}
		# tunnel
		else if(is_forward_2) {
			@dungeon_image_index := 11;	
		}
		else if(is_forward_1) {
			@dungeon_image_index := 7;		
		}
		else {
			@dungeon_image_index := 3;
		};

#		ShowMap();
	}

	method : CheckPlayerPosition(player_y : Int, player_x : Int) ~ Bool {
		map_dimensions := @map->Size();

		map_dimension_x := map_dimensions[1];
		map_dimension_y := map_dimensions[0];

		if(player_y < 0 | player_y >= map_dimension_y) {
			return false;
		};

		if(player_x < 0 | player_x >= map_dimension_x) {
			return false;
		};

		if(@map[player_y, player_x] = 'X' | @map[player_y, player_x] = '@' | @map[player_y, player_x] = 'S') {
			return true;
		}
		else {
			return false;
		};
	}

	method : LoadMap() ~ Nil {
		map_path := "./media/debug.dat";
		map_file := FileReader->New(map_path);
		leaving {
			map_file->Close();
		};
		
		size_line := map_file->ReadString();		
		rows := cols := size_line->ToInt();

		@map := Char->New[rows, cols];
		@map_start := Int->New[2];
		@map_end := Int->New[2];

		for(i := 0; i < rows; i += 1;) {
			line := map_file->ReadString();
			if(line->Size() <> cols) {
				"--- Invalid map cols! ---"->ErrorLine();
				Runtime->Exit(1);
			};

			each(j : line) {
				value := line->Get(j);				
				if(value = 'S') {
					@map_start[0] := i;
					@map_start[1] := j;
				}
				else if(value = 'E') {
					@map_end[0] := i;
					@map_end[1] := j;	
				};
				
				@map[i,j] := value;
			};
		};

		@dungeon_image_index := 0;
		@player_pos := @map_start;
		@player_dir := Direction->NORTH;

		UpdateDungeonView();
	}

	method : ShowMap() ~ Nil {
		map_dimensions := @map->Size();

		player_x := @player_pos[1];
		player_y := @player_pos[0];

		for(i := 0; i < map_dimensions[0]; i += 1;) {
			for(j := 0; j < map_dimensions[0]; j += 1;) {
				value := @map[i,j];
				if(i = player_y & j = player_x) {
					select(@player_dir) {
	        			label Direction->NORTH {
	        				value := '↑';
	        			}

	        			label Direction->SOUTH {
	        				value := '↓';
	        			}

	        			label Direction->EAST {
	        				value := '→';
	        			}

	        			label Direction->WEST {
	        				value := '←';
	        			}
	        		};
				};
				"{$value} "->Print();
			};
			"\n"->Print();
		};
		"--- {$player_x},{$player_y} ---\n"->PrintLine();
	}

	# ------- Game Logic -------

	method : StartFight() ~ Nil {
		@monster := Slime->New();
		@monster_percent := @monster->GetHpPercentage();
	}

	method : Fight(type : AttackType) ~ Nil {
		@hit_view_timer := 10;

		player_attack := @player->MakeAttack();
		player_defense := @player->MakeDefence();

		monster_attack := @monster->MakeAttack();
		monster_defense := @monster->MakeDefence();

		player_diff := monster_attack - player_defense;
		monster_diff := player_attack - monster_defense;

		player_hp := @player->GetHp();
		@player_percent := @player->GetHpPercentage();	

		monster_hp := @monster->GetHp();
		@monster_percent := @monster->GetHpPercentage();	

"Player Hit: attack={$monster_attack}, defense={$player_defense} -> [{$player_diff}, {$player_hp}, {$@player_percent}]"->PrintLine();
"Monster Hit: attack={$player_attack}, defense={$monster_defense} -> [{$monster_diff}, {$monster_hp}, {$@monster_percent}]"->PrintLine();

		if(@player->SetHp(player_diff, true)) {
			"--- DEAD ---"->PrintLine();
		};

		if(@monster->SetHp(monster_diff, true)) {
			@monster := Nil;
		};
	}
}

class Player from Entity {
	@experience : Int;

	New() {
		Parent(EntityType->SLIME,	# type
			1, 						# level
			Int->Random(8, 12),		# max hp
			Int->Random(4, 5),		# attack
			Int->Random(3, 4));		# defense
	}

	method : public : Rest() ~ Nil {
		@hp += @max_hp * @levels[Int->Random(0, 2)];
		if(@hp > @max_hp) {
			@hp := @max_hp;
		};
	}
}

class Slime from Entity {
	New() {
		Parent(EntityType->SLIME,	# type
			0,						# level
			Int->Random(3, 5),		# max hp
			Int->Random(4, 6),		# attack
			Int->Random(5, 6));		# defense
	}
}

class Entity {
	@type : EntityType;
	@level : Int;
	@max_hp : Int;
	@hp : Int;
	@attack : Int;
	@defense : Int;
	@levels : Float[];
	
	New(type : EntityType, level : Int, max_hp : Int, attack : Int, defense : Int) {
		@type := type;
		@level := level;
		@max_hp := max_hp;
		@hp := @max_hp;
		@attack := attack;
		@defense := defense;

		factor := 0.45;
		@levels := Float->New[10];
		each(i : @levels) {
			@levels[i] := factor;
			factor += 0.2;
		};
	}

	method : public : GetHpPercentage() ~ Float {
		return @hp->As(Float) / @max_hp->As(Float);
	}

	method : public : GetHp() ~ Int {
		return @hp;
	}

	method : public : GetAttack() ~ Int {
		return @attack;
	}

	method : public : GetDefense() ~ Int {
		return @defense;
	}

	method : public : MakeAttack() ~ Int {
		select(@level) {
			label 0: {
				return @attack * @levels[Int->Random(2)];
			}

			label 1: {
				return @attack * @levels[Int->Random(1, 3)];
			}

			other: {
				return @attack;
			}
		};
	}

	method : public : MakeDefence() ~ Int {
		select(@level) {
			label 0: {
				return @defense * @levels[Int->Random(2)];
			}

			label 1: {
				return @defense * @levels[Int->Random(1, 3)];
			}

			other: {
				return @defense;
			}
		};
	}

	method : public : SetHp(adjustment : Int, is_attack : Bool) ~ Bool {
		if(is_attack) {
			if(adjustment <= 0) {
				return false;
			};
			@hp -= adjustment;
		}
		else {
			@hp += adjustment;
		};

		if(@hp < 1) {
			@hp := 0;
			return true;
		};

		if(@hp > @max_hp) {
			@hp := @max_hp;
		};

		return false;
	}
}

consts ScreenConsts {
	SCREEN_WIDTH := 800,
	SCREEN_HEIGHT := 420,
	DUNGEON_IMAGE_MAX := 14,
	MONSTER_IMAGE_MAX := 3,
	MAP_ICON_MAX := 2,
	ACTIONS_ICON_MAX := 8
}

enum AttackType {
	SWORD,
	SPELL
}

enum EntityType {
	PERSON,
	SLIME,
	RAT
}

enum Direction {
	NORTH,
	SOUTH,
	EAST,
	WEST
}