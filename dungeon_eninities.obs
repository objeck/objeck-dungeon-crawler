#~
Tiny Dungeon Crawler
Copyright (c) 2020 Randy Hollines
~#

use System.Concurrency;
use Collection.Generic;
use Game.SDL2;
use Game.Framework;
use System.IO.File;

#~
Ghost
~#
class Ghost from Entity {
	New(level : Int) {
		Parent(EntityType->GHOST,		# type
			level,						# level
			Int->Random(9, 13),			# max hp
			Int->Random(17, 19),		# strength
			Int->Random(11, 13),		# agility
			Int->Random(7, 10));		# stamina
	}

	method : public : GetName() ~ String {
		return "Slime";
	}

	method : public : GetFightExperince() ~ Int {
		return Int->Random(9, 11);
	}

	method : public : GetImageStart() ~ Int {
		return 8;
	}

	method : public : GetImageEnd() ~ Int {
		return 11;
	}
}

#~
Slime
~#
class Slime from Entity {
	New(level : Int) {
		Parent(EntityType->SLIME,	# type
			level,					# level
			Int->Random(7, 11),		# max hp
			Int->Random(14, 16),	# strength
			Int->Random(10, 11),	# agility
			Int->Random(14, 15));	# stamina
	}

	method : public : GetName() ~ String {
		return "Slime";
	}

	method : public : GetFightExperince() ~ Int {
		return Int->Random(9, 11);
	}

	method : public : GetImageStart() ~ Int {
		return 4;
	}

	method : public : GetImageEnd() ~ Int {
		return 7;
	}
}

#~
Rat
~#
class Rat from Entity {
	New(level : Int) {
		Parent(EntityType->RAT,		# type
			level,					# level
			Int->Random(5, 10),		# max hp
			Int->Random(15, 17),	# strength
			Int->Random(14, 15),	# agility
			Int->Random(8, 10));		# stamina
	}

	method : public : GetName() ~ String {
		return "Rat";
	}

	method : public : GetFightExperince() ~ Int {
		return Int->Random(12, 15);
	}

	method : public : GetImageStart() ~ Int {
		return 0;
	}

	method : public : GetImageEnd() ~ Int {
		return 3;
	}
}

# --- Weapons ---
interface Weapon {
	method : virtual : public : GetName() ~ String;
	method : virtual : public : GetHit() ~ Float;
}

class WoodenSword implements Weapon {
	New() {
	}

	method : public : GetName() ~ String {
		return "Wooden Sword";
	}

	method : public : GetHit() ~ Float {
		return 0.2;
	}
}

# --- Armor ---
interface Armor {
	method : virtual : public : GetName() ~ String;
	method : virtual : public : GetHit() ~ Float;
}

class Leather implements Armor {
	New() {
	}

	method : public : GetName() ~ String {
		return "Leather";
	}

	method : public : GetHit() ~ Float {
		return 0.15;
	}
}

# --- Player ---

class Player from Entity {
	@experience : Int;
	@mp : Int;
	@gold : Int;
	@heal_potion_count : Int;
	@tent_count : Int;
	@weapon : Weapon;
	@armor : Armor;

	New() {
		Parent(EntityType->PERSON,	# type
			1, 						# level
			Int->Random(21, 28),	# max hp
			Int->Random(14, 17),	# strength
			Int->Random(12, 14),	# agility
			Int->Random(13, 15));	# stamina

		@mp := Int->Random(4, 7);
		@heal_potion_count := Int->Random(3, 4);
		@tent_count := Int->Random(1, 2);
	}

	method : public : SetWeapon(weapon : Weapon) ~ Nil {
		@weapon := weapon;
	}

	method : public : SetArmor(armor : Armor) ~ Nil {
		@armor := armor;
	}

	method : public : GetWeapon() ~ Weapon {
		return @weapon;
	}

	method : public : GetArmor() ~ Armor {
		return @armor;
	}

	method : public : AddExperince(monster : Entity) ~ Nil {
		@experience += monster->GetFightExperince();
	}

	method : public : GetExperince() ~ Int {
		return @experience;
	}

	method : public : AddGold(value : Int) ~ Nil {
		@gold += value;
	}

	method : public : GetGold() ~ Int {
		return @gold;
	}

	method : public : GetAc() ~ Int {
		return -1 * (@strength * 0.45 + @agility * 0.25 + @armor_block * 0.4);
	}

	method : public : GetMp() ~ Int {
		return @mp;
	}

	method : public : GetLevel() ~ Int {
		if(@experience > 300 & @level < 5) {
			SetMaxHp(GetMaxHp() * 1.10);
			SetHp(@max_hp, true);
			@level := 5;
		}
		else if(@experience <= 300 & @experience > 200 & @level < 4) {
			SetMaxHp(GetMaxHp() * 1.15);
			SetHp(@max_hp, true);
			@level := 4;
		}
		else if(@experience <= 200 & @experience > 130 & @level < 3) {
			SetMaxHp(GetMaxHp() * 1.20);
			SetHp(@max_hp, true);
			@level := 3;
		}
		else if(@experience <= 130 & @experience > 50 & @level < 2) {
			SetMaxHp(GetMaxHp() * 1.25);
			SetHp(@max_hp, true);
			@level := 2;
		}
		else if(@level = 1) {
			@level := 1;
		};

		return @level;
		
	}

	method : public : HasTent() ~ Bool {
		return @tent_count > 0;
	}

	method : public : UseTent() ~ Nil {
		if(@tent_count > 0) {
			@tent_count -= 1;
			@hp += Int->Random(0, 1) = 0 ? @max_hp * 0.33 : @max_hp * 0.66;
			if(@hp > @max_hp) {
				SetHp(@max_hp, true);
			};
		};
	}

	method : public : UseHealPotion() ~ Nil {
			if(@heal_potion_count > 0) {
				@heal_potion_count -= 1;
				SetHp(Int->Random(0, 1) = 0 ? @max_hp * 0.25 : @max_hp * 0.5, true);
				if(@hp > @max_hp) {
					SetHp(@max_hp, true);
				};
			};
	}

	method : public : GetHealPotionCount() ~ Int {
		return @heal_potion_count;
	}

	method : public : GetTentCount() ~ Int {
		return @tent_count;
	}

	method : public : GetName() ~ String {
		return "Player";
	}

	method : public : GetFightExperince() ~ Int {
		return @experience;
	}

	method : public : GetImageEnd() ~ Int {
		return -1;
	}

	method : public : GetImageStart() ~ Int {
		return -1;
	}
}

class Entity {
	@type : EntityType;
	@level : Int;
	@max_hp : Int;
	@hp : Int;
	
	@strength : Float;
	@agility : Float;
	@stamina : Float;
	@weapon_hit : Float;
	@armor_block : Float;
	@levels : Float[];
	@image_index : Int;
	@debug : Bool;

	@level_mutex : static : ThreadMutex;

	enum EntityType {
		PERSON,
		SLIME,
		RAT,
		GHOST
	}

	function : Init() ~ Nil {
		@level_mutex := ThreadMutex->New("level_lock");
	}
	
	New(type : EntityType, level : Int, max_hp : Int, strength : Int, agility : Int, stamina : Int) {
		@type := type;
		@level := level;
		@max_hp := @hp := max_hp;
		@max_hp := max_hp;
		@strength := strength;
		@agility := agility;
		@stamina := stamina;
		@debug := true;

		base := 0.75;
		@levels := Float->New[30];
		each(i : @levels) {
			@levels[i] := base;
			base += 0.05;
		};
	}

	method : virtual : public : GetName() ~ String;

	method : virtual : public : GetFightExperince() ~ Int;

	method : virtual : public : GetImageEnd() ~ Int;

	method : virtual : public : GetImageStart() ~ Int;

	method : public : GetHpPercentage() ~ Float {
		return @hp->As(Float) / @max_hp->As(Float);
	}

	method : public : IsAlive() ~ Bool {
		return @hp > 0;
	}

	method : public : GetHp() ~ Int {
		return @hp;
	}

	method : public : GetMaxHp() ~ Int {
		return @max_hp;
	}

	method : public : SetMaxHp(max_hp : Int) ~ Nil {
		@max_hp := max_hp;
	}

	method : public : GetAttack() ~ Int {
		return (@strength * 0.45 + @agility * 0.25 + @weapon_hit * 0.4) * @levels[Int->Random(@level - 1, @level + 1)];
	}

	method : public : GetDefense() ~ Int {
		return (@stamina * 0.35 + @agility * 0.25 + @armor_block * 0.4) * @levels[Int->Random(@level - 1, @level + 1)];
	}

	method : public : Attack(opponent : Entity) ~ Bool {
		attack := GetAttack();
		defense := opponent->GetDefense();
		
		diff := attack - defense;
		opponent->SetHp(diff < 0 ? Int->Random(0, 1) : diff);

		if(@debug) {
			our_name := GetName();
			opponent_name := opponent->GetName();
			"{$our_name} => {$opponent_name}: attack={$attack}, defense={$defense} -> {$diff}\n---"->PrintLine();
		};

		return diff > 0;
	}

	method : public : SetHp(adjustment : Int, is_potion : Bool := false) ~ Bool {
		critical(@level_mutex) {
			if(<>is_potion) {
				if(adjustment <= 0) {
					return false;
				};
				@hp -= adjustment;
			}
			else {
				@hp += adjustment;
			};

			if(@hp < 1) {
				@hp := 0;
				return true;
			};

			if(@hp > @max_hp) {
				@hp := @max_hp;
			};
		};

		return false;
	}
}