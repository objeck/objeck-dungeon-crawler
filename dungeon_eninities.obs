#~
Tiny Dungeon Crawler
Copyright (c) 2020, 2022 Randy Hollines
~#

use System.Concurrency;
use Collection;
use Game.SDL2;
use Game.Framework;
use System.IO.Filesystem;

# --- Magic ---
class LightHeal from Magic {
	New() {
		Parent(Int->Random(9, 12), 0, 0, 0);
	}

	method : public : ToString() ~ String {
		return "Light Heal";
	}
}

class SmallMissle from Magic {
	New() {
		Parent(0, 0, 0, Int->Random(4, 6));
	}

	method : public : ToString() ~ String {
		return "Small Missile";
	}
}

class Magic {
	New(hp : Int, mp : Int, enc : Int, dam : Int) {

	}

	method : virtual : public : ToString() ~ String;
}

# --- Potions ---
class Potion {
	@type : Potion->Type;
	@count : Int;
	@hp : Int;
	@mp : Int;
	@enc : Int;

	enum Type {
		WEAK_HEALTH,
		WEAK_MAGIC,
		WEAK_INVISIBILITY
	}

	New(type : Potion->Type) {
		@type := type;
		select(type) {
			label Potion->Type->WEAK_HEALTH {
				@hp := Int->Random(4, 8);
				@mp := @enc := 0;
			}

			label Potion->Type->WEAK_MAGIC {
				@mp := Int->Random(3, 5);
				@hp := @enc := 0;

			}

			label Potion->Type->WEAK_INVISIBILITY {
				@hp := @mp := 0;
				@enc := Int->Random(5, 9);
			}
		};
	}

	method : public : GetHp() ~ Int {
		return @hp;
	}

	method : public : GetMp() ~ Int {
		return @mp;
	}

	method : public : GetEnc() ~ Int {
		return @enc;
	}

	method : public : Add(count : Int) ~ Nil {
		@count += count;
	}

	method : public : GetType() ~ Potion->Type {
		return @type;
	}

	method : public : GetCount() ~ Int {
		return @count;
	}

	method : public : ReduceCount() ~ Bool {
		if(@count > 0) {
			@count -= 1;
			return true;
		};

		return false;
	}

	method : public : ToString() ~ String {
		select(@type) {
			label Potion->Type->WEAK_HEALTH {
				return "Weak Health";
			}

			label Potion->Type->WEAK_MAGIC {
				return "Weak Magic";
			}

			label Potion->Type->WEAK_INVISIBILITY {
				return "Weak Invisibility";
			}
		};

		return "Unknown";
	}
}

# --- Weapons ---
class WoodenSword implements Weapon {
	New() {
	}

	method : public : GetHit() ~ Float {
		return 27.5;
	}

	method : public : ToString() ~ String {
		return "Wooden Sword";
	}
}

interface Weapon {
	method : virtual : public : GetHit() ~ Float;
	method : virtual : public : ToString() ~ String;
}

# --- Armor ---
class LeatherTunic implements Armor {
	New() {
	}

	method : public : GetBlock() ~ Float {
		return 15.5;
	}

	method : public : ToString() ~ String {
		return "Leather Tunic";
	}
}

interface Armor {
	method : virtual : public : GetBlock() ~ Float;
	method : virtual : public : ToString() ~ String;
}

# --- Player ---

class Player from Entity {
	@name : String;
	@xp : Int;
	@xp_thresholds : Int[];

	@max_mp : Int;
	@mp : Int;

	@max_enc : Int;
	@enc : Int;

	@gold : Int;
	@tents : Int;

	@weapon : Weapon;
	@armor : Armor;

	@potions : Potion[];
	@magics : Magic[];

	New(name : String) {
		Parent(EntityType->PERSON,	# type
			1, 						# level
			Int->Random(21, 28),	# max hp
			Int->Random(12, 18),	# strength
			Int->Random(9, 13),		# agility
			Int->Random(9, 15));	# stamina

		@name := name;
		@xp := 0;
		@mp := @max_mp := Int->Random(7, 13);

		@xp_thresholds := Int->New[10];
		@xp_thresholds[0] := 0;
		@xp_thresholds[1] := 25;
		@xp_thresholds[2] := 60;
		@xp_thresholds[3] := 110;
		@xp_thresholds[4] := 180;
		@xp_thresholds[5] := 270;
		@xp_thresholds[6] := 380;
		@xp_thresholds[7] := 520;
		@xp_thresholds[8] := 700;
		@xp_thresholds[9] := 920;

		@potions := Potion->New[3];
		@magics := Magic->New[2];
	}

	#
	# --- Level-Up ---
	#
	method : public : AddXP(amount : Int) ~ Bool {
		@xp += amount;

		if(@level < 9 & @xp >= @xp_thresholds[@level]) {
			@level += 1;
			@max_hp += 4;
			@hp := @max_hp;
			@strength += 1.5;
			@stamina += 1.0;
			return true;
		};

		return false;
	}

	method : public : GetLevel() ~ Int {
		return @level;
	}

	method : public : GetXP() ~ Int {
		return @xp;
	}

	#
	# --- Potions ---
	#
	method : public : AddPotion(type : Potion->Type, count : Int) ~ Bool {
		found := false;

		for(i := 0; <>found & i < @potions->Size(); i += 1;) {
			potion := @potions[i];
			if(potion <> Nil & potion->GetType() = type) {
				potion->Add(count);
				found := true;
			};
		};

		if(<>found) {
			for(i := 0; <>found & i < @potions->Size(); i += 1;) {
				potion := @potions[i];
				if(potion = Nil) {
					@potions[i] := Potion->New(type);
					@potions[i]->Add(count);
					found := true;
				};
			};
		};

		return found;
	}

	method : public : UsePotion(index : Int) ~ Bool {
		if(index > -1 & index < @potions->Size()) {
			potion := @potions[index];
			if(potion <> Nil & potion->ReduceCount()) {
				@hp += potion->GetHp();
				if(@hp > @max_hp) {
					@hp := @max_hp;
				};

				@mp += potion->GetMp();
				if(@mp > @max_mp) {
					@mp := @max_mp;
				};

				@enc += potion->GetEnc();

				return true;
			};
		};

		return false;
	}

	method : public : GetPotion1() ~ Potion {
		return @potions[0];
	}

	method : public : GetPotion2() ~ Potion {
		return @potions[1];
	}

	method : public : GetPotion3() ~ Potion {
		return @potions[2];
	}

	#
	# --- Magic ---
	#
	method : public : UseMagic(index : Int) ~ Nil {
	}

	method : public : GetMagic1() ~ Magic {
		return @magics[0];
	}

	method : public : SetMagic1(magic1 : Magic) ~ Nil {
		@magics[0] := magic1;
	}

	method : public : GetMagic2() ~ Magic {
		return @magics[1];
	}

	method : public : SetMagic2(magic2 : Magic) ~ Nil {
		@magics[1] := magic2;
	}

	#
	# --- Tents ---
	#
	method : public : AddTent() ~ Nil {
		@tents += 1;
	}

	method : public : GetTents() ~ Int {
		return @tents;
	}

	#
	# --- Weapons and Armor ---
	#
	method : public : SetWeapon(weapon : Weapon) ~ Nil {
		@weapon := weapon;
		SetWeaponHit(@weapon->GetHit());
	}

	method : public : SetArmor(armor : Armor) ~ Nil {
		@armor := armor;
		SetArmorBlock(@armor->GetBlock());
	}

	method : public : GetWeapon() ~ Weapon {
		return @weapon;
	}

	method : public : GetArmor() ~ Armor {
		return @armor;
	}

	#
	# --- Gold and Exp ---
	#
	method : public : AddExperince(monster : Entity) ~ Nil {
		@xp += monster->GetWinExp();
	}

	method : public : GetExperince() ~ Int {
		return @xp;
	}

	method : public : AddGold(value : Int) ~ Nil {
		@gold += value;
	}

	method : public : GetGold() ~ Int {
		return @gold;
	}

	#
	# --- Attributes ---
	#
	method : public : GetAc() ~ Int {
		return -1 * (@strength * 0.45 + @agility * 0.25 + @armor_block * 0.4);
	}

	method : public : GetMp() ~ Int {
		return @mp;
	}

	method : public : GetEnc() ~ Int {
		return @enc;
	}

	method : public : GetName() ~ String {
		return @name;
	}

	method : public : GetWinExp() ~ Int {
		return @xp;
	}

	method : public : GetHealth() ~ String {
		return "Healthy";
	}

	method : public : ToString() ~ String {
		ac := GetAc();
		return "{$@name}: HP={$@hp}, MP={$@mp}, AC={$ac}";
	}
}

class Entity {
	@type : EntityType;

	@level : Int;
	@max_hp : Int;
	@hp : Int;

	@strength : Float;
	@agility : Float;
	@stamina : Float;
	@weapon_hit : Float;
	@armor_block : Float;
	@levels : Float[];
	@image_index : Int;
	@debug : Bool;

	enum EntityType {
		PERSON,
		SLIME,
		RAT,
		GHOST
	}

	New(type : EntityType, level : Int, max_hp : Int, strength : Int, agility : Int, stamina : Int) {
		@type := type;
		@level := level;
		@hp := @max_hp := max_hp;
		@agility := agility;
		@stamina := stamina;
		@debug := true;

		base := 0.65;
		@levels := Float->New[30];
		each(i : @levels) {
			@levels[i] := base;
			base += 0.05;
		};
	}

	method : virtual : public : GetName() ~ String;

	method : virtual : public : GetWinExp() ~ Int;

	method : public : GetMaxHp() ~ Int {
		return @max_hp;
	}

	method : public : GetHpPerc() ~ Float {
		return @hp->As(Float) / @max_hp->As(Float);
	}

	method : public : IsAlive() ~ Bool {
		return @hp > 0;
	}

	method : public : GetHp() ~ Int {
		return @hp;
	}

	method : public : SetWeaponHit(weapon_hit : Float) ~ Nil {
		@weapon_hit := weapon_hit;
	}

	method : public : SetArmorBlock(armor_block : Float) ~ Nil {
		@armor_block := armor_block;
	}

	method : public : UpdateHp(update : Int) ~ Nil {
		@hp -= update;
	}

	method : public : GetAttack() ~ Int {
		return (@strength * 0.40 + @agility * 0.15 + @weapon_hit * 0.45) * @levels[Int->Random(@level - 1, @level + 1)];
	}

	method : public : GetDefense() ~ Int {
		return (@stamina * 0.35 + @agility * 0.25 + @armor_block * 0.40) * @levels[Int->Random(@level - 1, @level + 1)];
	}

	method : public : Attack(opponent : Entity) ~ Bool {
		attack := GetAttack();
		defense := opponent->GetDefense();

		diff := attack - defense;
		opponent->UpdateHp(diff < 0 ? Int->Random(0, 1) : diff);

		if(@debug) {
			our_name := GetName();
			opponent_name := opponent->GetName();
			"{$our_name} => {$opponent_name}: attack={$attack}, defense={$defense} -> {$diff}\n---"->PrintLine();
		};

		return diff > 0;
	}
}

# --- Monsters ---
class Monster from Entity {
	New(type : EntityType, level : Int, max_hp : Int, strength : Int, agility : Int, stamina : Int) {
		Parent(type, level, max_hp, strength, agility, stamina);
	}

	method : virtual : public : GetImageStart() ~ Int;

	method : public : GetImageEnd() ~ Int {
		return GetImageStart() + 4;
	}

	method : virtual : public : GetName() ~ String;

	method : virtual : public : GetWinExp() ~ Int;

	method : virtual : public : IsBoss() ~ Bool;
}

#~
Ghost
~#
class Ghost from Monster {
	New(level : Int) {
		Parent(EntityType->GHOST,	# type
			level,					# level
			Int->Random(10, 12),	# max hp
			Int->Random(10, 11),	# strength
			Int->Random(7, 8),		# agility
			Int->Random(6, 8));		# stamina

		SetWeaponHit(35.3);
		SetArmorBlock(13.3);
	}

	method : public : GetName() ~ String {
		return "Ghost";
	}

	method : public : GetWinExp() ~ Int {
		return Int->Random(9, 11);
	}

	method : public : GetImageStart() ~ Int {
		return 8;
	}

	method : public : IsBoss() ~ Bool {
		return false;
	}
}

#~
Slime
~#
class Slime from Monster {
	New(level : Int) {
		Parent(EntityType->SLIME,	# type
			level,					# level
			Int->Random(11, 13),	# max hp
			Int->Random(7, 9),		# strength
			Int->Random(6, 8),		# agility
			Int->Random(7, 9));		# stamina

		SetWeaponHit(32.1);
		SetArmorBlock(15.4);
	}

	method : public : GetName() ~ String {
		return "Slime";
	}

	method : public : GetWinExp() ~ Int {
		return Int->Random(9, 11);
	}

	method : public : GetImageStart() ~ Int {
		return 4;
	}

	method : public : IsBoss() ~ Bool {
		return false;
	}
}

#~
Rat
~#
class Rat from Monster {
	New(level : Int) {
		Parent(EntityType->RAT,		# type
			level,					# level
			Int->Random(6, 9),		# max hp
			Int->Random(11, 13),	# strength
			Int->Random(9, 11),		# agility
			Int->Random(5, 6));		# stamina

		SetWeaponHit(39.5);
		SetArmorBlock(10.9);
	}

	method : public : GetName() ~ String {
		return "Rat";
	}

	method : public : GetWinExp() ~ Int {
		return Int->Random(12, 15);
	}

	method : public : GetImageStart() ~ Int {
		return 0;
	}

	method : public : IsBoss() ~ Bool {
		return false;
	}
}

#~
Dungeon Lord - Boss Monster
~#
class DungeonLord from Monster {
	New(level : Int) {
		Parent(EntityType->GHOST,	# type (reuses Ghost sprites)
			level,					# level
			Int->Random(50, 60),	# max hp
			Int->Random(16, 18),	# strength
			Int->Random(10, 12),	# agility
			Int->Random(12, 14));	# stamina

		SetWeaponHit(48.0);
		SetArmorBlock(22.0);
	}

	method : public : GetName() ~ String {
		return "Dungeon Lord";
	}

	method : public : GetWinExp() ~ Int {
		return 100;
	}

	method : public : GetImageStart() ~ Int {
		return 8;
	}

	method : public : IsBoss() ~ Bool {
		return true;
	}
}
