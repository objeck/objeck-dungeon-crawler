#~
Tiny Dungeon Crawler
Copyright (c) 2020 Randy Hollines
~#

use Collection.Generic;
use Game.SDL2;
use Game.Framework;

class TinyDungeon {
	@framework : GameFramework;
	@sprite_manager : SpriteManager;
	@sound_manager : SoundManager;
	@map_manager : MapManager;

	@player_location : Int[];
	@player_direction : MapManager->Direction;

	@dungeon_views : Int[];

	New() {
		@framework := GameFramework->New(Game->SCREEN_WIDTH, Game->SCREEN_HEIGHT, "Tiny Dungeon");
		@framework->SetClearColor(Color->New(50, 60, 57));

		@sprite_manager := SpriteManager->New("./images", @framework);
		@sound_manager := SoundManager->New("./sounds", @framework);
		@map_manager := MapManager->New("./maps");

		@player_location := Int->New[3];
		@dungeon_views := Int->New[SpriteManager->Clips->DUNGEON_X_CLIPS + 1];
	}

	function : Main(args : String[]) ~ Nil {
		TinyDungeon->New()->Run();
	}

	method : Run() ~ Nil {
		if(<>@map_manager->Load()) {
			"--- Unable to load maps! ---"->ErrorLine();
			return;
		};

		if(<>@sprite_manager->Load()) {
			"--- Unable to load sprites! ---"->ErrorLine();
			return;
		};

		# TODO: add sound manager

		leaving {
			@map_manager->Unload();
			@sprite_manager->Unload();
			@sound_manager->Unload();
		};

		if(@framework->IsOk()) {
			StartLevel();

			frame_count := 0;
			quit := false;
			e := @framework->GetEvent();
			while(<>quit) {
				@framework->FrameStart();
				
				# process input
				while(e->Poll() <> 0) {
					# quit
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					}
					# keyboard
					else if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
				        GetInput(e);
				    };
				};

				Render(frame_count);

				@framework->FrameEnd();

				frame_count += 1;
				if(frame_count >= @framework->GetFps()) {
					frame_count := 0;
				};
			};
		}
		else {
			"--- Error Initializing Environment ---"->ErrorLine();
			return;
		};
	}

	method : StartLevel() ~ Nil {
		@player_direction := MapManager->Direction->NORTH;
		start_location := @map_manager->GetStart();
		@player_location[0] := start_location[0];
		@player_location[1] := start_location[1];

		UpdateView();
	}

	method : GetInput(e : Event) ~ Nil {
		select(e->GetKey()->GetKeysym()->GetScancode()) {
        	label Scancode->SDL_SCANCODE_UP
        	label Scancode->SDL_SCANCODE_W {
        		MoveForward();
        		UpdateView();
        	}

        	label Scancode->SDL_SCANCODE_LEFT
        	label Scancode->SDL_SCANCODE_A {
        		MoveLeft();
        		UpdateView();
        	}

        	label Scancode->SDL_SCANCODE_RIGHT
        	label Scancode->SDL_SCANCODE_D {
        		MoveRight();
        		UpdateView();
        	}
        };
	}

	method : MoveForward() ~ Nil {
		player_view := @map_manager->GetView(@player_direction, @player_location);
		if(IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->MIDDLE])) {
			select(@player_direction) {
				label MapManager->Direction->NORTH {
					@player_location[0] -= 1;
				}

				label MapManager->Direction->SOUTH {
					@player_location[0] += 1;
				}

				label MapManager->Direction->EAST {
					@player_location[1] += 1;
				}

				label MapManager->Direction->WEST {
					@player_location[1] -= 1;
				}
			};
		};
	}

	method : native : UpdateView() ~ Nil {
		is_blocked := false;
		player_view := @map_manager->GetView(@player_direction, @player_location, true);
		each(i : @dungeon_views) {
			@dungeon_views[i] := -1;
		};

		#
		# --- View 0 ---
		#
		if(IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->MIDDLE]) & 
				IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->LEFT]) & 				
				IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->RIGHT])) {
			@dungeon_views[0] := 15;
"--- V0: Open to left and right ---"->PrintLine();
		}
		else if(IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->MIDDLE]) &
					IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->LEFT])) {
			@dungeon_views[0] := 11;
"--- V0: Open to left ---"->PrintLine();
		}
		else if(IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->MIDDLE]) & 
					IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->RIGHT])) {
			@dungeon_views[0] := 7;
"--- V0: Open to right ---"->PrintLine();
		}
		else if(IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->MIDDLE])) {
			@dungeon_views[0] := 3;
"--- V0: Enclosed ---"->PrintLine();
		}
		else {
"--- V0: Blocked ---"->PrintLine();
		};

		#
		# --- View 1 ---
		#
		if(IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->MIDDLE]) & 
				IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->LEFT]) & 				
				IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->RIGHT])) {
			@dungeon_views[1] := 14;
"--- V1: Open to left and right ---"->PrintLine();
		}
		else if(IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->MIDDLE]) &
					IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->LEFT])) {
			# open space
			if(IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->MIDDLE]) &
					IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->LEFT])) {
				@dungeon_views[0] := 23;
				@dungeon_views[1] := 22;
			}
			else {
				@dungeon_views[1] := 10;
			};
"--- V1: Open to left ---"->PrintLine();
		}
		else if(IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->MIDDLE]) & 
					IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->RIGHT])) {
			if(IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->MIDDLE]) & 
					IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->RIGHT])) {
				@dungeon_views[0] := 19;
				@dungeon_views[1] := 18;
			}
			else {
				@dungeon_views[1] := 6;
			};
"--- V1: Open to right ---"->PrintLine();
		}
		else if(IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->MIDDLE])) {
			@dungeon_views[1] := 2;
"--- V1: Enclosed ---"->PrintLine();
		}
		else {
			@dungeon_views[4] := 31;
			is_blocked := true;
"--- V1: Blocked ---"->PrintLine();
		};

		#
		# --- View 2 ---
		#
		if(<>is_blocked) {
			if(IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->MIDDLE]) & 
					IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->LEFT]) & 				
					IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->RIGHT])) {
				@dungeon_views[2] := 13;
"--- V2: Open to left and right ---"->PrintLine();
			}
			else if(IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->MIDDLE]) &
						IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->LEFT])) {
				@dungeon_views[2] := 9;
"--- V2: Open to left ---"->PrintLine();
			}
			else if(IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->MIDDLE]) & 
						IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->RIGHT])) {
				@dungeon_views[2] := 5;
"--- V2: Open to right ---"->PrintLine();
			}
			else if(IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->MIDDLE])) {
				@dungeon_views[2] := 1;
"--- V2: Enclosed ---"->PrintLine();
			}
			else {
				@dungeon_views[4] := 30;
				is_blocked := true;
"--- V2: Blocked ---"->PrintLine();
			};
		};

		#
		# --- View 3 ---
		#		
		if(<>is_blocked) {
			if(IsOpen(player_view[MapManager->View->VIEW_3, MapManager->Side->MIDDLE]) & 
					IsOpen(player_view[MapManager->View->VIEW_3, MapManager->Side->LEFT]) & 				
					IsOpen(player_view[MapManager->View->VIEW_3, MapManager->Side->RIGHT])) {
				@dungeon_views[3] := 12;
"--- V3: Open to left and right ---"->PrintLine();
			}
			else if(IsOpen(player_view[MapManager->View->VIEW_3, MapManager->Side->MIDDLE]) &
					IsOpen(player_view[MapManager->View->VIEW_3, MapManager->Side->LEFT])) {
				@dungeon_views[3] := 8;
"--- V3: Open to left ---"->PrintLine();
			}
			else if(IsOpen(player_view[MapManager->View->VIEW_3, MapManager->Side->MIDDLE]) & 
					IsOpen(player_view[MapManager->View->VIEW_3, MapManager->Side->RIGHT])) {
				@dungeon_views[3] := 4;
"--- V3: Open to right ---"->PrintLine();
			}
			else if(IsOpen(player_view[MapManager->View->VIEW_3, MapManager->Side->MIDDLE])) {
				@dungeon_views[3] := 0;
"--- V3: Enclosed ---"->PrintLine();
			}
			else {
				@dungeon_views[4] := 29;
				is_blocked := true;
"--- V3: Blocked ---"->PrintLine();
			};
		};

		#
		# --- View 4 ---
		#
		if(<>is_blocked & <>IsOpen(player_view[MapManager->View->VIEW_4, MapManager->Side->MIDDLE])) {				
			@dungeon_views[4] := 28;
"--- V4: Blocked"->PrintLine();
		}; 			

#        @map_manager->PrintMap(@player_direction, @player_location);
	}

	method : IsOpen(player_view : Int) ~ Bool {
		if(player_view >= MapManager->Type->OPEN & player_view <= MapManager->Type->END) {
			return true;
		};

		return false;
	}


	method : IsVisible(view : MapManager->View, player_view : Int[,]) ~ Bool {
		count := 0;

		for(i := 0; i < 3; i += 1;) {
			if(player_view[view, i] = MapManager->Type->BLOCKED) {
				count += 1;
			};
		};

		return count <> 3;
	}

	method : MoveLeft() ~ Nil {
		select(@player_direction) {
			label MapManager->Direction->NORTH {
				@player_direction := MapManager->Direction->WEST;
			}

			label MapManager->Direction->SOUTH {
				@player_direction := MapManager->Direction->EAST;
			}

			label MapManager->Direction->EAST {
				@player_direction := MapManager->Direction->NORTH;
			}

			label MapManager->Direction->WEST {
				@player_direction := MapManager->Direction->SOUTH;
			}
		};
	}

	method : MoveRight() ~ Nil {
		select(@player_direction) {
			label MapManager->Direction->NORTH {
				@player_direction := MapManager->Direction->EAST;
			}

			label MapManager->Direction->SOUTH {
				@player_direction := MapManager->Direction->WEST;
			}

			label MapManager->Direction->EAST {
				@player_direction := MapManager->Direction->SOUTH;
			}

			label MapManager->Direction->WEST {
				@player_direction := MapManager->Direction->NORTH;
			}
		};
	}

	method : Render(frame_count : Int) ~ Nil {
		@framework->Clear();

		# background
		@sprite_manager->GetDungeonSprites()->Render(@dungeon_views[4]);
		for(i := 0; i < 4; i += 1;) {
			index := @dungeon_views[i];
			if(index > -1) {
				@sprite_manager->GetDungeonSprites()->Render(index);
			};
		};
		
		@framework->Show();
	}

	consts Game {
		SCREEN_WIDTH := 1024,
		SCREEN_HEIGHT := 512
	}
}