#~
Tiny Dungeon Crawler
Copyright (c) 2020 Randy Hollines
~#

use Collection.Generic;
use Game.SDL2;
use Game.Framework;

class TinyDungeon {
	@framework : GameFramework;
	@sprite_manager : SpriteManager;
	@sound_manager : SoundManager;
	@map_manager : MapManager;

	@player_location : Int[];
	@player_direction : MapManager->Direction;

	@dungeon_views : Int[];

	New() {
		@framework := GameFramework->New(Game->SCREEN_WIDTH, Game->SCREEN_HEIGHT, "Tiny Dungeon");
		@framework->SetClearColor(Color->New(50, 60, 57));

		@sprite_manager := SpriteManager->New("./images", @framework);
		@sound_manager := SoundManager->New("./sounds", @framework);
		@map_manager := MapManager->New("./maps");

		@player_location := Int->New[3];
		@dungeon_views := Int->New[SpriteManager->Clips->DUNGEON_X_CLIPS + 2];
	}

	function : Main(args : String[]) ~ Nil {
		TinyDungeon->New()->Run();
	}

	method : Run() ~ Nil {
		if(<>@map_manager->Load()) {
			"--- Unable to load maps! ---"->ErrorLine();
			return;
		};

		if(<>@sprite_manager->Load()) {
			"--- Unable to load sprites! ---"->ErrorLine();
			return;
		};

		# TODO: add sound manager

		leaving {
			@map_manager->Unload();
			@sprite_manager->Unload();
			@sound_manager->Unload();
		};

		if(@framework->IsOk()) {
			StartLevel();

			frame_count := 0;
			quit := false;
			e := @framework->GetEvent();
			while(<>quit) {
				@framework->FrameStart();
				
				# process input
				while(e->Poll() <> 0) {
					# quit
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					}
					# keyboard
					else if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
				        GetInput(e);
				    };
				};

				Render(frame_count);

				@framework->FrameEnd();

				frame_count += 1;
				if(frame_count >= @framework->GetFps()) {
					frame_count := 0;
				};
			};
		}
		else {
			"--- Error Initializing Environment ---"->ErrorLine();
			return;
		};
	}

	method : StartLevel() ~ Nil {
		@player_direction := MapManager->Direction->NORTH;
		start_location := @map_manager->GetStart();
		@player_location[0] := start_location[0];
		@player_location[1] := start_location[1];

		UpdateView();
	}

	method : GetInput(e : Event) ~ Nil {
		select(e->GetKey()->GetKeysym()->GetScancode()) {
        	label Scancode->SDL_SCANCODE_UP
        	label Scancode->SDL_SCANCODE_W {
        		MoveForward();
        		UpdateView();
        	}

        	label Scancode->SDL_SCANCODE_LEFT
        	label Scancode->SDL_SCANCODE_A {
        		MoveLeft();
        		UpdateView();
        	}

        	label Scancode->SDL_SCANCODE_RIGHT
        	label Scancode->SDL_SCANCODE_D {
        		MoveRight();
        		UpdateView();
        	}
        };
	}

	method : MoveForward() ~ Nil {
		player_view := @map_manager->GetView(@player_direction, @player_location);
		if(IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->MIDDLE])) {
			select(@player_direction) {
				label MapManager->Direction->NORTH {
					@player_location[0] -= 1;
				}

				label MapManager->Direction->SOUTH {
					@player_location[0] += 1;
				}

				label MapManager->Direction->EAST {
					@player_location[1] += 1;
				}

				label MapManager->Direction->WEST {
					@player_location[1] -= 1;
				}
			};
		};
	}

	method : IsClosedLeftOpenRight(view : MapManager->View, player_view : Int[,]) ~ Bool {
		return <>IsOpen(player_view[view, MapManager->Side->LEFT]) & 
			IsOpen(player_view[view, MapManager->Side->RIGHT]) &
			IsOpen(player_view[view, MapManager->Side->MIDDLE]);				
	}

	method : IsClosedRightOpenLeft(view : MapManager->View, player_view : Int[,]) ~ Bool {
		return IsOpen(player_view[view, MapManager->Side->LEFT]) & 
			<>IsOpen(player_view[view, MapManager->Side->RIGHT]) &
			IsOpen(player_view[view, MapManager->Side->MIDDLE]);				
	}

	method : IsClosedLeftRight(view : MapManager->View, player_view : Int[,]) ~ Bool {
		return <>IsOpen(player_view[view, MapManager->Side->LEFT]) & 
			<>IsOpen(player_view[view, MapManager->Side->RIGHT]) &
			IsOpen(player_view[view, MapManager->Side->MIDDLE]);				
	}

	method : IsOpenLeft(view : MapManager->View, player_view : Int[,]) ~ Bool {
		return IsOpen(player_view[view, MapManager->Side->LEFT]) & 
			IsOpen(player_view[view, MapManager->Side->MIDDLE]);				
	}

	method : IsOpenRight(view : MapManager->View, player_view : Int[,]) ~ Bool {
		return IsOpen(player_view[view, MapManager->Side->RIGHT]) & 
			IsOpen(player_view[view, MapManager->Side->MIDDLE]);				
	}

	method : IsOpenLeftRight(view : MapManager->View, player_view : Int[,]) ~ Bool {
		return IsOpen(player_view[view, MapManager->Side->LEFT]) & 
			IsOpen(player_view[view, MapManager->Side->RIGHT]) & 
			IsOpen(player_view[view, MapManager->Side->MIDDLE]);				
	}

	method : native : UpdateView() ~ Nil {
		player_view := @map_manager->GetView(@player_direction, @player_location, true);
		each(i : @dungeon_views) {
			@dungeon_views[i] := -1;
		};

		is_blocked := false;

		#
		# View 0
		#
		if(IsOpenLeftRight(MapManager->View->VIEW_0, player_view)) {
			@dungeon_views[1] := 15;
		}
		else if(IsOpenLeft(View->VIEW_0, player_view)) {
			@dungeon_views[1] := 11;
		}
		else if(IsOpenRight(MapManager->View->VIEW_0, player_view)) {
			@dungeon_views[1] := 7;
		}
		else if(IsOpen(player_view[MapManager->View->VIEW_0, MapManager->Side->MIDDLE])) {
			@dungeon_views[1] := 3;
		};

		#
		# View 1
		#
		if(IsOpenLeftRight(MapManager->View->VIEW_1, player_view)) {
			# V1: middle, left and right
			if(IsOpenLeftRight(MapManager->View->VIEW_0, player_view) &
					IsOpenLeftRight(MapManager->View->VIEW_1, player_view) & 
					IsClosedLeftRight(MapManager->View->VIEW_2, player_view)) {
				@dungeon_views[1] := 23;
				@dungeon_views[2] := 94;
			}
			else if(IsOpenLeftRight(MapManager->View->VIEW_0, player_view)) {
				@dungeon_views[1] := 23;
				@dungeon_views[2] := 22;
			}
			else if(IsClosedLeftOpenRight(MapManager->View->VIEW_0, player_view)) {
				@dungeon_views[2] := 70;
			}
			else if(IsClosedRightOpenLeft(MapManager->View->VIEW_0, player_view)) {
				@dungeon_views[2] := 82;
			}
			else {
				@dungeon_views[2] := 14;
			};
		}
		# V1: left
		else if(IsOpenLeft(View->VIEW_1, player_view)) {			
			if(IsOpenLeft(View->VIEW_0, player_view)) {
				@dungeon_views[1] := 27;
				if(IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->MIDDLE])) {
					@dungeon_views[2] := 110;
				}
				else {
					@dungeon_views[2] := 26;
				};
			}
			else {
"--- 0 ---"->PrintLine();
				@dungeon_views[2] := 10;
			};
		}
		# V1: right
		else if(IsOpenRight(MapManager->View->VIEW_1, player_view)) {
			if(IsOpenRight(MapManager->View->VIEW_0, player_view)) {
				@dungeon_views[1] := 31;
				if(IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->MIDDLE])) {
					@dungeon_views[2] := 114;
				}
				else {
					@dungeon_views[2] := 30;
				};
			}
			else {
				@dungeon_views[2] := 6;
			};
		}
		# V1: middle
		else if(IsOpen(player_view[MapManager->View->VIEW_1, MapManager->Side->MIDDLE])) {
			@dungeon_views[2] := 2;
		}
		else {
			@dungeon_views[0] := 19;
			is_blocked := true;
		};

		#
		# View 2
		#
		if(<>is_blocked) {
			# V2: middle, left and right
			if(IsOpenLeftRight(MapManager->View->VIEW_2, player_view)) {
				if(IsOpenLeftRight(MapManager->View->VIEW_1, player_view)) {
					if(IsClosedLeftRight(MapManager->View->VIEW_0, player_view)) {
						@dungeon_views[3] := 33;
					}
					else if(IsClosedLeftRight(MapManager->View->VIEW_3, player_view)) {
						@dungeon_views[3] := 106;
					}
					else {
						@dungeon_views[3] := 21;
					};
				}
				else {
					if(IsOpenRight(MapManager->View->VIEW_1, player_view)) {
						@dungeon_views[3] := 102;
					}
					else if(IsOpenLeft(MapManager->View->VIEW_1, player_view)) {
						@dungeon_views[3] := 98;
					}
					else {
						@dungeon_views[3] := 13;
					};
				};
			}
			# V2: left
			else if(IsOpenLeft(View->VIEW_2, player_view)) {
				if(IsOpenLeft(View->VIEW_1, player_view)) {
					if(IsClosedLeftRight(MapManager->View->VIEW_0, player_view)) {
						@dungeon_views[3] := 45;
					}
					else if(IsOpenLeft(View->VIEW_0, player_view)) {
						@dungeon_views[3] := 109;
					}
					else {
						@dungeon_views[3] := 45; # 25;
					};
				}
				else {
					@dungeon_views[3] := 9;
				};
			}
			# V2: right
			else if(IsOpenRight(MapManager->View->VIEW_2, player_view)) {
				if(IsOpenRight(MapManager->View->VIEW_1, player_view)) {
					if(IsClosedLeftRight(MapManager->View->VIEW_0, player_view)) {
						@dungeon_views[3] := 57;
					}
					else if(IsOpenRight(View->VIEW_0, player_view)) {
						@dungeon_views[3] := 113;
					}
					else {
						@dungeon_views[3] := 57; #29?
					};
				}
				else {
"--- 1 ---"->PrintLine();
					@dungeon_views[3] := 5;
				};
			}
			# V2: middle
			else if(IsOpen(player_view[MapManager->View->VIEW_2, MapManager->Side->MIDDLE])) {
				@dungeon_views[3] := 1;
			}
			else {
				@dungeon_views[0] := 18;
				is_blocked := true;
			};
		};

		#
		# View 3
		#
		if(<>is_blocked) {
			# V3: middle, left and right
			if(IsOpenLeftRight(MapManager->View->VIEW_3, player_view)) {				
				if(IsOpenLeftRight(MapManager->View->VIEW_1, player_view) & 
						IsOpenLeftRight(MapManager->View->VIEW_2, player_view)) {
					@dungeon_views[4] := 32;
				}
				else if(IsOpenLeftRight(MapManager->View->VIEW_2, player_view)) {
					@dungeon_views[3] := 37;
					@dungeon_views[4] := 36;
				}
				else if(IsOpenLeftRight(MapManager->View->VIEW_1, player_view)) {
					if(IsClosedLeftRight(MapManager->View->VIEW_0, player_view)) {
						@dungeon_views[4] := 32;
					}
					else if(IsClosedLeftRight(MapManager->View->VIEW_2, player_view)) {
						@dungeon_views[4] := 40;
					}
					else {
						@dungeon_views[4] := 20;
					};
				}
				else if(IsOpenLeftRight(MapManager->View->VIEW_4, player_view) & 
							IsOpenLeftRight(MapManager->View->VIEW_3, player_view)) {
					@dungeon_views[4] := 40;
				}
				else if(IsOpenLeftRight(MapManager->View->VIEW_3, player_view)) {
					@dungeon_views[4] := 12;
				}
				else {
					@dungeon_views[4] := 36;
				};
			}
			# V3: left
			else if(IsOpenLeft(View->VIEW_3, player_view)) {				
				if(IsOpenLeft(View->VIEW_1, player_view)) {
					if(IsClosedLeftRight(MapManager->View->VIEW_0, player_view)) {
						@dungeon_views[4] := 44;
					}
					else if(IsOpenLeft(MapManager->View->VIEW_3, player_view) & 
								IsOpenRight(MapManager->View->VIEW_3, player_view)) {
						@dungeon_views[4] := 40;
					}
					else if(IsOpenLeft(MapManager->View->VIEW_3, player_view)) {
						@dungeon_views[4] := 44;
					}
					else if(IsOpenRight(MapManager->View->VIEW_3, player_view)) {
						@dungeon_views[4] := 64;
					}
					else {
						@dungeon_views[4] := 24;
					};
				}
				else if(IsOpenLeft(MapManager->View->VIEW_2, player_view)) {
					@dungeon_views[3] := 49;
					@dungeon_views[4] := 48;
				}
				else if(IsOpenLeft(MapManager->View->VIEW_3, player_view)) {
					@dungeon_views[4] := 52;
				}
				else {
					@dungeon_views[4] := 8;
				};
			}
			# V3: right
			else if(IsOpenRight(MapManager->View->VIEW_3, player_view)) {
				if(IsOpenRight(MapManager->View->VIEW_1, player_view)) {
					if(IsClosedLeftRight(MapManager->View->VIEW_0, player_view)) {
						@dungeon_views[4] := 56;
					}
					else if(IsOpenLeft(MapManager->View->VIEW_3, player_view) & 
								IsOpenRight(MapManager->View->VIEW_3, player_view)) {
						@dungeon_views[4] := 40;
					}
					else if(IsOpenLeft(MapManager->View->VIEW_3, player_view)) {
						@dungeon_views[4] := 52;
					}
					else if(IsOpenRight(MapManager->View->VIEW_3, player_view) & 
							IsOpenRight(MapManager->View->VIEW_2, player_view)) {
						@dungeon_views[4] := 56;
					}
					else if(IsOpenRight(MapManager->View->VIEW_3, player_view)) {
						@dungeon_views[4] := 64;
					}
					else {
						@dungeon_views[4] := 28;
					};
				}
				else if(IsOpenRight(MapManager->View->VIEW_2, player_view)) {
					@dungeon_views[3] := 61;
					@dungeon_views[4] := 60;
				}
				else if(IsOpenRight(MapManager->View->VIEW_3, player_view) & 
							IsOpenRight(MapManager->View->VIEW_2, player_view)) {
					@dungeon_views[4] := 56;
				}
				else if(IsOpenRight(MapManager->View->VIEW_3, player_view)) {
					@dungeon_views[4] := 64;
				}
				else {
					@dungeon_views[4] := 4;
				};
			}
			# V3: middle
			else if(IsOpen(player_view[MapManager->View->VIEW_3, MapManager->Side->MIDDLE])) {
				@dungeon_views[4] := 0;
			}
			else {
				@dungeon_views[0] := 17;
				is_blocked := true;
			};
		};

		#
		# View 4
		#
		if(<>is_blocked) {
			if(<>IsOpen(player_view[MapManager->View->VIEW_4, MapManager->Side->MIDDLE])) {
				@dungeon_views[0] := 16;
			}
			else {				
				@dungeon_views[0] := 116; # change me!
			};
		};

        @map_manager->PrintMap(@player_direction, @player_location);

        DebugView();
	}

	method : DebugView() ~ Nil {
		"\nViews\n-----"->PrintLine();
		each(i : @dungeon_views) {
			image_id := @dungeon_views[i];
			if(image_id > -1) {
				"Level {$i}: image={$image_id}"->PrintLine();
			};
		};
	}

	method : IsOpen(player_view : Int) ~ Bool {
		if(player_view >= MapManager->Type->OPEN & player_view <= MapManager->Type->END) {
			return true;
		};

		return false;
	}


	method : IsVisible(view : MapManager->View, player_view : Int[,]) ~ Bool {
		count := 0;

		for(i := 0; i < 3; i += 1;) {
			if(player_view[view, i] = MapManager->Type->BLOCKED) {
				count += 1;
			};
		};

		return count <> 3;
	}

	method : MoveLeft() ~ Nil {
		select(@player_direction) {
			label MapManager->Direction->NORTH {
				@player_direction := MapManager->Direction->WEST;
			}

			label MapManager->Direction->SOUTH {
				@player_direction := MapManager->Direction->EAST;
			}

			label MapManager->Direction->EAST {
				@player_direction := MapManager->Direction->NORTH;
			}

			label MapManager->Direction->WEST {
				@player_direction := MapManager->Direction->SOUTH;
			}
		};
	}

	method : MoveRight() ~ Nil {
		select(@player_direction) {
			label MapManager->Direction->NORTH {
				@player_direction := MapManager->Direction->EAST;
			}

			label MapManager->Direction->SOUTH {
				@player_direction := MapManager->Direction->WEST;
			}

			label MapManager->Direction->EAST {
				@player_direction := MapManager->Direction->SOUTH;
			}

			label MapManager->Direction->WEST {
				@player_direction := MapManager->Direction->NORTH;
			}
		};
	}

	method : Render(frame_count : Int) ~ Nil {
		@framework->Clear();

		# background
		each(i : @dungeon_views) {
			@sprite_manager->RenderDungeonSprite(@dungeon_views[i]);
		};
#		@sprite_manager->RenderDungeonSprite(100);
		
		@framework->Show();
	}

	consts Game {
		SCREEN_WIDTH := 1024,
		SCREEN_HEIGHT := 512
	}
}