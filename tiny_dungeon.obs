use Collection.Generic;
use Game.SDL2;
use Game.Framework;

class TinyDungeon {
	@framework : GameFramework;
	@sprite_manager : SpriteManager;
	@sound_manager : SoundManager;
	@map_manager : MapManager;

	New() {
		@framework := GameFramework->New(Game->SCREEN_WIDTH, Game->SCREEN_HEIGHT, "Tiny Dungeon");
		@framework->SetClearColor(Color->New(34, 32, 52));

		@sprite_manager := SpriteManager->New("./images", @framework);
		@sound_manager := SoundManager->New("./sounds", @framework);
		@map_manager := MapManager->New("./maps");
	}

	function : Main(args : String[]) ~ Nil {
		TinyDungeon->New()->Run();
	}

	method : Debug() ~ Nil {
		start := @map_manager->GetStart();
		y := start[0]; x := start[1];
		"start: {$y},{$x}"->PrintLine();

		view := @map_manager->GetView(MapManager->Direction->WEST, y, x);
		x := view[2, MapManager->Side->LEFT]; 
		y := view[2, MapManager->Side->MIDDLE]; 
		z := view[2, MapManager->Side->RIGHT];
		"test: {$x},{$y},{$z}"->PrintLine();
	}

	method : Run() ~ Nil {
		if(<>@map_manager->Load()) {
			"--- Unable to load maps! ---"->ErrorLine();
			return;
		};

		if(<>@sprite_manager->Load()) {
			"--- Unable to load sprites! ---"->ErrorLine();
			return;
		};

		leaving {
			@map_manager->Unload();
			@sprite_manager->Unload();
			@sound_manager->Unload();
		};

		if(@framework->IsOk()) {
			# TODO: disable me
			Debug();

			frame_count := 0;
			quit := false;
			e := @framework->GetEvent();
			while(<>quit) {
				@framework->FrameStart();
				
				# process input
				while(e->Poll() <> 0) {
					# quit
					if(e->GetType() = EventType->SDL_QUIT) {
						quit := true;
					}
					# keyboard
					else if(e->GetType() = EventType->SDL_KEYDOWN & e->GetKey()->GetRepeat() = 0) {
				        GetInput(e);
				    };
				};

				Render(frame_count);

				@framework->FrameEnd();

				frame_count += 1;
				if(frame_count >= @framework->GetFps()) {
					frame_count := 0;
				};
			};
		}
		else {
			"--- Error Initializing Environment ---"->ErrorLine();
			return;
		};
	}

	method : GetInput(e : Event) ~ Nil {
		select(e->GetKey()->GetKeysym()->GetScancode()) {
        	label Scancode->SDL_SCANCODE_UP
        	label Scancode->SDL_SCANCODE_W {
        	}

        	label Scancode->SDL_SCANCODE_LEFT
        	label Scancode->SDL_SCANCODE_A {
        	}

        	label Scancode->SDL_SCANCODE_RIGHT
        	label Scancode->SDL_SCANCODE_D {
        	}
        };
	}

	method : Render(frame_count : Int) ~ Nil {
		@framework->Clear();

		@sprite_manager->GetDungeonSprites()->Render(0);
		@sprite_manager->GetDungeonSprites()->Render(1);
		@sprite_manager->GetDungeonSprites()->Render(2);
		@sprite_manager->GetDungeonSprites()->Render(3);
		@sprite_manager->GetDungeonSprites()->Render(4);

		@framework->Show();
	}

	consts Game {
		SCREEN_WIDTH := 1280,
		SCREEN_HEIGHT := 720
	}
}